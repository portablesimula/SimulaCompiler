
% ===============================================   Main   program =============
%  PROGRAM NAME:   Simula a.s. - SIMULA TEST NO 61
%
%  PURPOSE:     Test remote access to attributes which are protected.
%
%  WRITTEN BY:  BEH   04/08-86
% ==============================================================================

begin
	integer nnn=61; text title="--- Test remote access to attributes which are protected.";
	
	boolean found_error;
	integer nFailed;
	boolean verbose=false;
	boolean noMessage;

	procedure err(t); text t;
	begin sysout.setpos(1);
	      outtext("*** error: in test "); outtext(t); outimage;
	      found_error:=true; nFailed:=nFailed+1;
	end;

   class A;
   begin
      integer i;
      integer procedure vai; vai := i;
   end;

   A class B;
      protected i;
   begin
      integer i;

      integer procedure vbi; vbi := i;

      procedure p; x.i := 1;
   end;

   ref(B) x;


	if verbose then begin
		outtext ("--- START Simula a.s. TEST"); outint(nnn,4); outimage;
		outtext (title);
		outimage; outimage;
	end;


   x :- new B;
   x.p;      ! i.e. B'i := 1;
   x.i := 2; ! i.e. A'i := 2;

   if x.vai = 2 and x.vbi = 1 then
   else
   begin
      err("Visibility of protected attribute(1).");
      outtext("            Erroneus values : x.vai = ");
      outint(x.vai, 4); outimage;
      outtext("                              x.vbi = ");
      outint(x.vbi, 4); outimage;
   end;

   B begin
      x.i := 3;
      i := 4;

      if x.vbi = 3 and vbi = 4 then
      else
      begin
         err("*** error : Visibility of protected attribute(2).");
         outtext("             Erroneus values : x.vbi = ");
         outint(x.vbi, 4); outimage;
         outtext("                                 vbi = ");
         outint(vbi, 4); outimage;
      end;
   end;


	if noMessage then else begin
		if not found_error then begin
			outtext ("--- NO ERRORS FOUND IN TEST"); outint(nnn,4); outtext("  "); outtext(title); outimage;
		end;

		if verbose then begin
			outtext ("--- END Simula a.s. TEST"); outint(nnn,4); outimage;
		end;
	end;

end of program;
