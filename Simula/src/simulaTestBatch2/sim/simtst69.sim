
% ===============================================   Main   program =============
%  PROGRAM NAME:   Simula a.s. - SIMULA TEST NO 69
%
%  Annotated example from Simula Standard sect. 7.3.1
%
%  PURPOSE:     Test complex use of detach, call and resume.
%
%  WRITTEN BY:  MYH   28/07-2018
% ==============================================================================
external class SimulaTest;

SimulaTest(69,"--- Test complex use of detach, call and resume.") begin
   
   integer n;
   text array facit,notes(0:8);
   
   procedure prt(msg); text msg;
   begin if verbose then 
      begin outtext(msg); outimage; end;
   end;
   
   procedure note(msg); text msg;
   begin notes(n):-msg;
      if notes(n) <> facit(n) then 
         prt("*** ERROR: '" & notes(n) & "' <> '" & facit(n) & "'");
      n:=n+1;
      prt(msg);
   end;   

   comment *** SYSTEM: S1;
      ref(C1) X1;
      class C1;
      begin procedure P1; detach;
            note("Before Detach C1 - see fig. 7.1");
            P1;
      end C1;
      ref(C2) X2;
      class C2;
      begin procedure P2;
            begin
               note("Before Detach C2 - see fig. 7.6");
               detach;
               note("After  Detach C2 - see fig. 7.6 again");
            end P2;
            begin comment *** SYSTEM S2;
                  ref(C3) X3;
                  class C3;
                  begin note("Before Detach C3 - see fig. 7.3");
                        detach;
                        note("After  Resume X3 - see fig. 7.5");
                        P2;
                  end C3;
                  X3:- new C3;
                  note("After  Detach C3 - see fig. 7.4");
                  resume(X3)
            end S2;
      end C2;
      
      
      
      
      prt("--------------------------------------------------------");
      prt("Begin Annotated example from Simula Standard sect. 7.3.1");
      prt("--------------------------------------------------------");
      prt("Expected Output:");
      prt("--------------------------------------------------------");
      facit(0):-"Before Detach C1 - see fig. 7.1"; prt(facit(0));
      facit(1):-"After  Detach C1 - see fig. 7.2"; prt(facit(1));
      facit(2):-"Before Detach C3 - see fig. 7.3"; prt(facit(2));
      facit(3):-"After  Detach C3 - see fig. 7.4"; prt(facit(3));
      facit(4):-"After  Resume X3 - see fig. 7.5"; prt(facit(4));
      facit(5):-"Before Detach C2 - see fig. 7.6"; prt(facit(5));
      facit(6):-"After  Detach C2 - see fig. 7.7"; prt(facit(6));
      facit(7):-"After  Detach C2 - see fig. 7.6 again"; prt(facit(7));
      facit(8):-"Final END of Example"; prt(facit(8));
      prt("--------------------------------------------------------");
      prt("Actual Output:");
      prt("--------------------------------------------------------");
      
      X1:- new C1;
      note("After  Detach C1 - see fig. 7.2");
      X2:- new C2;
      note("After  Detach C2 - see fig. 7.7");
      call(X2);
      note("Final END of Example");
      prt("--------------------------------------------------------");

      
end S1;