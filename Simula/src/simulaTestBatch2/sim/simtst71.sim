
% ===============================================   Main   program =============
%  PROGRAM NAME:   Simula a.s. - SIMULA TEST NO 71
%
%  PURPOSE:     Test visibility of identifiers.
%
%  WRITTEN BY:  BEH   22/08-86
% ==============================================================================

begin
	integer nnn=71; text title="--- Test visibility of identifiers.";
	
	boolean found_error;
	integer nFailed;
	boolean verbose=false;
	boolean noMessage;

	procedure err(t); text t;
	begin sysout.setpos(1);
	      outtext("*** error: in test "); outtext(t); outimage;
	      found_error:=true; nFailed:=nFailed+1;
	end;


   integer i;

   class A;
      virtual : procedure P1;
   begin
      procedure P1; i := 1;
   end;

   A class B;
   begin
      procedure P1; i := 2;
      procedure P2; i := 3;
   end;

   ref(A) rA;

   procedure P2; i := 4;



	if verbose then begin
		outtext ("--- START Simula a.s. TEST"); outint(nnn,4); outimage;
		outtext (title);
		outimage; outimage;
	end;

   rA :- new B;

   inspect rA do
   begin P2 end;

   if i = 4 then else begin
      err("Visibility of identifiers(1).");
      outtext("            Value of i : ");
      outint(i, 3); outimage; outimage;
   end;

   rA :- new B;

   inspect rA when B do
   begin P2 end;

   if i = 3 then else begin
      err("Visibility of identifiers(2).");
      outtext("            Value of i : ");
      outint(i, 3); outimage; outimage;
   end;


   rA :- new B;

   inspect rA do
   begin P1 end;

   if i = 2 then else begin
      err("Visibility of identifiers(3).");
      outtext("            Value of i : ");
      outint(i, 3); outimage; outimage;
   end;


   rA :- new A;

   inspect rA do
   begin P1 end;


   if i = 1 then else begin
      err("Visibility of identifiers(4).");
      outtext("            Value of i : ");
      outint(i, 3); outimage; outimage;
   end;

   

	if noMessage then else begin
		if not found_error then begin
			outtext ("--- NO ERRORS FOUND IN TEST"); outint(nnn,4); outtext("  "); outtext(title); outimage;
		end;

		if verbose then begin
			outtext ("--- END Simula a.s. TEST"); outint(nnn,4); outimage;
		end;
	end;

end of program;

