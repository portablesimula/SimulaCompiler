
% ===============================================   Main   program =============
%  PROGRAM NAME:   SIMULA A/S - SIMULA TEST NO 13
%
%  PURPOSE:     To test text value assignment and text reference assigment.
%
%  METHOD:      Try and fail.
%
%  WRITTEN BY:  BEH   11/07-86
% ==============================================================================

begin
	integer n=13; text title="--- Test text value and text reference assigment.";
	
	boolean found_error;
	integer nFailed;
	boolean verbose=false;
	boolean noMessage;

   text t1, t2;

	procedure err(t); text t;
	begin sysout.setpos(1);
	      outtext("*** error: in test "); outtext(t); outimage;
	      found_error:=true; nFailed:=nFailed+1;
	end;

	if verbose then begin
		outtext ("--- START Simula a.s. TEST"); outint(n,4); outimage;
		outtext (title);
		outimage; outimage;
	end;


                                                !******************************;
   t1 :- blanks (3) ;                           ! Test text value assignment.  ;
   t1 := "abc" ;                                !******************************;

   if     t1 = "abc"
      and t1 <> t2 then
   else err ("*** error : Text value assignment (1). ");

   t2 :- blanks (5);
   t2 := t1;

   if     t1 <> t2
      and t2 = "abc  " then
   else err ("*** error : Text value assignment (2). ");

                                                !******************************;
                                                ! Test text reference assigment;
                                                !******************************;
   t1 :- notext;

   if     t1 = ""
      and t1.main == notext
      and t1.start = 1
      and t1.length = 0
      and t1.pos = 1 then
   else err ("*** error : Text reference assignment (3). ");

   t1 :- copy ("abc  ");

   if     t1 = t2
      and t1 =/= t2
      and t1.pos = 1
      and t1.start = 1
      and t1.length = 5 then
   else err ("*** error : Text reference assignment (4).");

   t2 :- t1;

   if     t1 = t2
      and t1 == t2
      and t2.start = 1
      and t2.pos = 1
      and t2.length = 5 then
   else err ("*** error : Text reference assignment (5).");


	if noMessage then else begin
		if not found_error then begin
			outtext ("--- NO ERRORS FOUND IN TEST"); outint(n,4); outtext("  "); outtext(title); outimage;
		end;

		if verbose then begin
			outtext ("--- END Simula a.s. TEST"); outint(n,4); outimage;
		end;
	end;

end of program;
