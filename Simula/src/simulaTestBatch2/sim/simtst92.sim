
begin
	integer nnn=92; text title="--- Test 1-dim Array by value.";
	
	boolean found_error;
	integer nFailed;
	boolean verbose=false;
	boolean noMessage;

	procedure err(t); text t;
	begin sysout.setpos(1);
	      outtext("*** error: in test "); outtext(t); outimage;
	      found_error:=true; nFailed:=nFailed+1;
	end;


   procedure fillArray(RA,v1); integer array RA; integer v1;
   begin integer i;
      for i:=lowerbound(RA,1) step 1 until upperbound(RA,1) do
      begin
         RA(i):=v1; v1:=v1+1;
      end;
   end;

   procedure checkArray(msg,RA,v1); text msg; integer array RA; integer v1;
   begin integer i;
      for i:=lowerbound(RA,1) step 1 until upperbound(RA,1) do
      begin
         if RA(i) ne v1 then
         begin
            outtext("*** ERROR " & msg & " at index "); outint(i,0);
            outtext(", RA="); outint(RA(i),0);
            outtext(", Facit="); outint(v1,0); outimage;
            err("Failed");
         end;
         v1:=v1+1;
      end;
   end;

   class A; virtual: procedure P;
   begin 
      procedure P(FA); value FA; integer array FA;
      begin
         fillArray(FA,654);
      end;
   end;
   
   A class B(PA); value PA; integer array PA;
   begin
      procedure Q(VA); value VA; integer array VA;
      begin
         fillArray(VA,987);
      end;
      
      fillArray(PA,321);
      
   end;   
   
   integer array RA(1:23);
   ref(B) x;

	if verbose then begin
		outtext ("--- START Simula a.s. TEST"); outint(nnn,4); outimage;
		outtext (title);
		outimage; outimage;
	end;
   
   fillArray(RA,123);
   x:-new B(RA);
   checkArray("After new B(RA)",RA,123);
   
   fillArray(RA,456);
   x.P(RA);
   checkArray("After Virtual: x.P(RA)",RA,456);

   fillArray(RA,789);
   x.Q(RA);
   checkArray("After Normal: x.Q(RA)",RA,789);
   

	if noMessage then else begin
		if not found_error then begin
			outtext ("--- NO ERRORS FOUND IN TEST"); outint(nnn,4); outtext("  "); outtext(title); outimage;
		end;

		if verbose then begin
			outtext ("--- END Simula a.s. TEST"); outint(nnn,4); outimage;
		end;
	end;

end of program;
   