
% ===============================================   Main   program =============
%  PROGRAM name:   Simula a.s. - SIMULA TEST NO 32
%
%  PURPOSE:     Simple test of formal procedures.
%
%  WRITTEN BY:  BEH   22/07-86
% ==============================================================================
begin
	integer n=32; text title="--- Simple test of formal procedures.";
	
	boolean found_error;
	integer nFailed;
	boolean verbose=false;
	boolean noMessage;

	procedure err(t); text t;
	begin sysout.setpos(1);
	      outtext("*** error: in test "); outtext(t); outimage;
	      found_error:=true; nFailed:=nFailed+1;
	end;

   class A ;
   begin
      integer ia ;
      integer procedure ip ;ip := 12 ;
   end of class A ;

   A class B ;
   begin
      integer ib ;
   end of class B ;

   B class C ;
   begin
      integer ic ;
   end of class C ;

   class X ; ;

   ref (A)            gra ;
   ref (A)            ra ;
   ref (B)            rb ;
   ref (C)            rc ;
   integer            gi ;
   real               gr ;
   character          gch ;
   boolean            gbool ;
   text               gtext ;

   integer procedure ip ;  ip := 11 ;

   real procedure rp ; ;

   procedure AP (i, r, bool, ch, ra, t) ;
                       integer     i ;
                       real        r ;
                       boolean     bool ;
                       character   ch ;
                       ref (A)     ra ;
                       text        t ;
   begin
      gi := i ;
      gr := r ;
      gbool := bool ;
      gch := ch ;
      gra :- ra ;
      gtext :- t
   end of procedure AP ;

   ref (A) procedure Rap ;  Rap :- new A ;

   procedure P (F) ;  procedure F ;
   begin
      F (1, 1.2, true, 'c', none, notext) ;

      if     gi = 1
%         and gr = 1.2  // MYH 29/3-2018 Test for equality between real values may not be meaningfull 
         and abs(gr-1.2)<0.0001
         and gbool
         and gch = 'c'
         and gra == none
         and gtext = notext
         
          then
      else
      begin
         err(" Value of global variables (1).") ;
         outimage ;
         outtext ("            Erroneus values : gi = ") ;
         outint (gi, 5) ;
         outimage ;
         outtext ("                              gr = ") ;
         outreal (gr, 5, 12) ;
         outimage ;
         outtext ("                           gbool = ") ;
         outtext (if gbool then "true" else "false") ;
         outimage ;
         outtext ("                             gch = ") ;
         outchar (gch) ;
         outimage ;
         outtext ("                             gra == ") ;
         outtext (if gra == none then "notext" else
                  if gra is A then "object of A" else
                  if gra is B then "object of B" else
                                   "object of C") ;
         outimage ;
         outtext ("                           gtext = ") ;
         outtext (if gtext = notext then "notext" else gtext) ;
         outimage ; outimage ;
      end ;

      F (ip, ip + 1, false, if true then 't' else 'f', Rap, copy ("abc")) ;

      if     gi = 11
%         and gr = 12  // MYH 29/3-2018 Test for equality between real values may not be meaningfull 
         and abs(gr-12)<0.0001
         and not gbool
         and gch = 't'
         and gra =/= none
         and gtext = "abc" then
      else
      begin
         err(" Value of global variables (2).") ;
         outimage ;
         outtext ("            Erroneus values : gi = ") ;
         outint (gi, 5) ;
         outimage ;
         outtext ("                              gr = ") ;
         outreal (gr, 5, 12) ;
         outimage ;
         outtext ("                           gbool = ") ;
         outtext (if gbool then "true" else "false") ;
         outimage ;
         outtext ("                             gch = ") ;
         outchar (gch) ;
         outimage ;
         outtext ("                             gra == ") ;
         outtext (if gra == none then "notext" else
                  if gra is A then "object of A" else
                  if gra is B then "object of B" else
                                   "object of C") ;
         outimage ;
         outtext ("                           gtext = ") ;
         outtext (if gtext = notext then "notext" else gtext) ;
         outimage ; outimage ;
      end ;

   end of procedure P ;



	if verbose then begin
		outtext ("--- START Simula a.s. TEST"); outint(n,4); outimage;
		outtext (title);
		outimage; outimage;
	end;


   P (AP) ;


	if noMessage then else begin
		if not found_error then begin
			outtext ("--- NO ERRORS FOUND IN TEST"); outint(n,4); outtext("  "); outtext(title); outimage;
		end;

		if verbose then begin
			outtext ("--- END Simula a.s. TEST"); outint(n,4); outimage;
		end;
	end;

end of program;

