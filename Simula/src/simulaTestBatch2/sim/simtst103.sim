
% ===============================================   Main   program =============
%  PROGRAM NAME:   SIMULA TEST NO 103
%
%  PURPOSE: All kinds of Activation Statements
%
%  WRITTEN BY:  MYH   28/04-2019
% ==============================================================================


Simulation begin
	integer nnn=103; text title="--- Test all kinds of Activation Statements";
	
	boolean found_error;
	integer nFailed;
	boolean verbose=true;
	boolean noMessage;

	procedure err(t); text t;
	begin sysout.setpos(1);
	      outtext("*** error: in test "); outtext(t); outimage;
	      found_error:=true; nFailed:=nFailed+1;
	end;

   ref(Car) bil1,bil2,bil3;
   text t;

   integer traceCase;
   text array facit(0:8);

   text procedure edfix(r); real r;
   begin text t; t:-blanks(6); t.putfix(r,2); edfix:-t; end;
       
   procedure trace(msg); text msg;
   begin
      text case1;
      case1:-edfix(time) & ": " & msg;
      if verbose then begin outtext(case1); outimage; end;
      if case1 <> facit(traceCase) then
      begin
         err("Case " & edfix(traceCase));
         outtext("Trace: " & case1); outimage;
         outtext("Facit: " & facit(traceCase)); outimage;
      end;
      traceCase:=traceCase+1;
   end;

   Process class Car(id); text id;
   begin
      trace(id & ": Run Fase-1");
      hold (4);
      trace(id & ": Run Fase-2");
      hold (12);
      trace(id & ": Terminates");
   end;
   

	if verbose then begin
		outtext ("--- START Simula a.s. TEST"); outint(nnn,4); outimage;
		outtext (title);
		outimage; outimage;
	end;
   
   facit(0):-"  0.00: Bil-1: Run Fase-1";
   facit(1):-"  4.00: Bil-1: Run Fase-2";
   facit(2):-"  7.45: Bil-3: Run Fase-1";
   facit(3):-"  7.45: Bil-2: Run Fase-1";                                                                                                           
   facit(4):-" 11.45: Bil-3: Run Fase-2";                                                                                                           
   facit(5):-" 11.45: Bil-2: Run Fase-2";                                                                                                           
   facit(6):-" 16.00: Bil-1: Terminates";                                                                                                           
   facit(7):-" 23.45: Bil-3: Terminates";                                                                                              
   facit(8):-" 23.45: Bil-2: Terminates";
   
   activate bil1; ! activate NONE is dummy ;

   bil1:-new Car("Bil-1");
   activate bil1;
   bil2:-new Car("Bil-2");
   bil3:-new Car("Bil-3");
   activate bil2 at 7.45;
   activate bil3 at 7.45 prior;
   activate bil2 delay 1.23;
   activate bil3 delay 1.23 prior;
   activate bil2 before bil1;
   activate bil3 after bil1;
   hold(100);
   

	if noMessage then else begin
		if not found_error then begin
			outtext ("--- NO ERRORS FOUND IN TEST"); outint(nnn,4); outtext("  "); outtext(title); outimage;
		end;

		if verbose then begin
			outtext ("--- END Simula a.s. TEST"); outint(nnn,4); outimage;
		end;
	end;

end of program;
   