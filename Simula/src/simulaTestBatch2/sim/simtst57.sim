
% ===============================================   Main   program =============
%  PROGRAM NAME:   Simula a.s. - SIMULA TEST NO 57
%
%  PURPOSE:     Test virtual procedure - different number of parameters in
%               the matching procedures in two subclasses(not fo each other)
%               of the prefix class which contains the virtual part.
%
%  WRITTEN BY:  BEH   01/08-86
% ==============================================================================

begin
	integer nnn=57; text title="--- Test virtual procedure - different number of parameters in match ...";
	
	boolean found_error;
	integer nFailed;
	boolean verbose=false;
	boolean noMessage;

	procedure err(t); text t;
	begin sysout.setpos(1);
	      outtext("*** error: in test "); outtext(t); outimage;
	      found_error:=true; nFailed:=nFailed+1;
	end;

   text t;


   class A;
      virtual: procedure Emit;
               procedure Dump;
   begin
      outtext("in A.*");
   end;

   A class AA;
   begin
      procedure Emit;
      begin
         outtext("in AA Emit.*");
      end;

      procedure Dump(rf);
         ref(A) rf;
      begin
         outtext("in AA Dump.*");
      end;

      outtext("in AA.*");
   end;

   A class AB;
   begin
      procedure Emit;
      begin
         outtext("in AB Emit.*");
      end;

      procedure Dump;
      begin
         outtext("in AB Dump.*");
      end;

      outtext("in AB.*");
   end;

   ref(A) rA;


	if verbose then begin
		outtext ("--- START Simula a.s. TEST"); outint(nnn,4); outimage;
		outtext (title);
		outimage; outimage;
	end;


   rA :- new AA;
   rA.Emit;
   rA.Dump(rA);

   t :- copy(sysout.image.strip);
   sysout.image.setpos(1);
   sysout.image := notext ;

   if t = "in A.*in AA.*in AA Emit.*in AA Dump.*" then
   else
   begin
      err("*** error : Wrong program execution(1).");
      outtext("            ");
      outtext(t); outimage;
   end;

   rA :- new AB;
   rA.Emit;
   rA.Dump;

   t :- copy(sysout.image.strip);
   sysout.image.setpos(1);
   sysout.image := notext ;

   if t = "in A.*in AB.*in AB Emit.*in AB Dump.*" then
   else
   begin
      err("*** error : Wrong program execution(2).");
      outtext("            ");
      outtext(t);  outimage;
   end;
   

	if noMessage then else begin
		if not found_error then begin
			outtext ("--- NO ERRORS FOUND IN TEST"); outint(nnn,4); outtext("  "); outtext(title); outimage;
		end;

		if verbose then begin
			outtext ("--- END Simula a.s. TEST"); outint(nnn,4); outimage;
		end;
	end;

end of program;

