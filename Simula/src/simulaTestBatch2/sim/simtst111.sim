% ===============================================   Main   program =============
%  PROGRAM NAME:   SIMULA TEST NO 111
%
%  PURPOSE: Virtual Label - Sample 1
%
%  WRITTEN BY:  MYH   28/04-2019
% ==============================================================================

begin
	integer nnn=111; text title="--- Test Virtual Label - Sample 1";
	
	boolean found_error;
	integer nFailed;
	boolean verbose=false;
	boolean noMessage;
   
   text procedure edint(i); integer i;
   begin text t; t:-blanks(6); t.putint(i); edint:-t; end;

	procedure err(t); text t;
	begin sysout.setpos(1);
	      outtext("*** error: in test "); outtext(t); outimage;
	      found_error:=true; nFailed:=nFailed+1;
	end;

   integer traceCase;
   text array facit(0:5);
   ref(B) x;
   
   procedure trace(msg); text msg;
   begin
      if verbose then begin outtext(msg); outimage; end;
      if msg <> facit(traceCase) then begin
         err("Case " & edint(traceCase));
         outtext("Trace: " & msg); outimage;
         outtext("Facit: " & facit(traceCase)); outimage;
      end;
      traceCase:=traceCase+1;
   end;


   class A; virtual: label L;
   begin
      text ident="CLASS A";
      trace("In A before inner");
      goto L;
      inner;
      trace("In A after inner");
   end;
   
   A class B;
   begin
      text ident="CLASS B";
      trace("In B before inner");
   L: trace("At Label L - OK");
      inner;
      trace("In B after inner");
   end;
   

	if verbose then begin
		outtext ("--- START Simula a.s. TEST"); outint(nnn,4); outimage;
		outtext (title);
		outimage; outimage;
	end;
   
   facit(0):-"In A before inner";
   facit(1):-"At Label L - OK";
   facit(2):-"In B after inner";
   facit(3):-"In A after inner";
   facit(4):-"In MAIN after new B";
   facit(5):-"In MAIN after new CLASS B";

   x:-new B;
   trace("In MAIN after new B");
   trace("In MAIN after new " & x.ident);
   
   

	if noMessage then else begin
		if not found_error then begin
			outtext ("--- NO ERRORS FOUND IN TEST"); outint(nnn,4); outtext("  "); outtext(title); outimage;
		end;

		if verbose then begin
			outtext ("--- END Simula a.s. TEST"); outint(nnn,4); outimage;
		end;
	end;

end of program;

