
% ===============================================   Main   program =============
%  PROGRAM NAME:   Simula a.s. - SIMULA TEST NO 60
%
%  PURPOSE:     Test visibility of protected attributes.
%
%  WRITTEN BY:  BEH   04/08-86,  MYH  27/05-2018
% ==============================================================================

begin
	integer nnn=60; text title="--- Test visibility of protected attributes.";
	
	boolean found_error;
	integer nFailed;
	boolean verbose=false;
	boolean noMessage;

	procedure err(t); text t;
	begin sysout.setpos(1);
	      outtext("*** error: in test "); outtext(t); outimage;
	      found_error:=true; nFailed:=nFailed+1;
	end;

   procedure check(expectedVai,expextedVbi,msg); integer expectedVai,expextedVbi; text msg; 
   begin
      if xb.vai = expectedVai and xb.vbi = expextedVbi then
      else
      begin
         err(" in " & msg & ": Visibility of protected attribute.");
         outtext("            Erroneus values : vai = "); outint(xb.vai, 4); outimage;
         outtext("                              vbi = "); outint(xb.vbi, 4); outimage;
      end;
   end;

   class A;
   begin integer i;
         integer procedure vai; vai := i;
   end;

   A class B; protected i;
   begin integer i;
         integer procedure vbi; vbi := i;
         procedure p; xb.i := 17;
   end;

   ref(B) xb;


	if verbose then begin
		outtext ("--- START Simula a.s. TEST"); outint(nnn,4); outimage;
		outtext (title);
		outimage; outimage;
	end;


   xb :- new B;
   xb.p;
   check(0,17,"Test 1");
   
   xb.i := 5;
   check(5,17,"Test 2");
   
   inspect xb do i := 7;
   check(7,17,"Test 3");


	if noMessage then else begin
		if not found_error then begin
			outtext ("--- NO ERRORS FOUND IN TEST"); outint(nnn,4); outtext("  "); outtext(title); outimage;
		end;

		if verbose then begin
			outtext ("--- END Simula a.s. TEST"); outint(nnn,4); outimage;
		end;
	end;

end of program;
