
% ===============================================   Main   program =============
%  PROGRAM NAME:   Simula a.s. - SIMULA TEST NO 74
%
%  PURPOSE:     Test resume and detach.
%
%  WRITTEN BY:  BEH   25/08-86
% ==============================================================================

begin
	integer nnn=74; text title="--- Test resume and detach.";
	
	boolean found_error;
	integer nFailed;
	boolean verbose=false;
	boolean noMessage;

	procedure err(t); text t;
	begin sysout.setpos(1);
	      outtext("*** error: in test "); outtext(t); outimage;
	      found_error:=true; nFailed:=nFailed+1;
	end;


   text t;

   procedure Check(c); character c;
         outchar(c);

   ref(A) x;
   ref(B) y;

   class A;
   begin real array X(P:1); Check('A');
         detach; Check('B');
         detach; Check('F');
   end;

   class B;
   begin real array X(P:1); Check('E');
         detach; Check('G');
         detach; Check('H');
   end;

   real procedure P;
   begin if x=/=none then resume(x); Check('C');
         if y=/=none then resume(y); Check('D');
   end;
   

	if verbose then begin
		outtext ("--- START Simula a.s. TEST"); outint(nnn,4); outimage;
		outtext (title);
		outimage; outimage;
	end;

   x:- new A;
   y:- new B;
   y:- new B;
   x:- none; x:- new A;
   resume(y);

   t:- copy(sysout.image.strip);
   sysout.setpos(1);
   sysout.image := notext;

   if t = "CDABCDEFCGDECGDAH" then else begin
      outtext("*** error : resume - detach.");
      outimage;
      outtext("            Erroneus sequencing: ");
      outtext(t);
      outimage; outimage
   end;
   

	if noMessage then else begin
		if not found_error then begin
			outtext ("--- NO ERRORS FOUND IN TEST"); outint(nnn,4); outtext("  "); outtext(title); outimage;
		end;

		if verbose then begin
			outtext ("--- END Simula a.s. TEST"); outint(nnn,4); outimage;
		end;
	end;

end of program;

