% ===============================================   Main   program =============
%  PROGRAM NAME:   SIMULA TEST NO 127
%
%  PURPOSE: Switch Statement
%
%  WRITTEN BY:  MYH   28/04-2019
% ==============================================================================

begin
	integer nnn=127; text title="--- Test switch statement";
	
	boolean found_error;
	integer nFailed;
	boolean verbose=false;
	boolean noMessage;
   
   text procedure edint(i); integer i;
   begin text t; t:-blanks(6); t.putint(i); edint:-t; end;

	procedure err(t); text t;
	begin sysout.setpos(1);
	      outtext("*** error: in test "); outtext(t); outimage;
	      found_error:=true; nFailed:=nFailed+1;
	end;

   integer traceCase;
   text array facit(0:9);
   
   procedure trace(msg); text msg;
   begin
      if verbose then begin outtext(msg); outimage; end;
      if msg <> facit(traceCase) then
      begin err("Case " & edint(traceCase));
         outtext("Trace: " & msg); outimage;
         outtext("Facit: " & facit(traceCase)); outimage;
      end;
      traceCase:=traceCase+1;
   end;
   
   procedure test1(key,msg); character key; text msg;
   begin
      character lowKey='!0!';
      character hiKey='Z';
      character case1='A';
      character case2='B';

      switch(lowKey:hiKey) key
      begin
         when case1 do begin trace(msg & " selected case 1"); end;
         when case2 do begin trace(msg & " selected case 2"); end;
         when 'C','D' do begin trace(msg & " selected case 3"); end;
         when none do begin trace(msg & " selected case none"); end;
      end case;
    end;
   
  
   procedure test2(key,msg); integer key; text msg;
   begin
      integer lowKey=0;
      integer hiKey=30;
      integer case1=1;
      integer case2=2;

      switch(lowKey:hiKey) key
      begin
         when case1 do begin trace(msg & " selected case 1"); end;
         when case2 do begin trace(msg & " selected case 2"); end;
         when 3,4,5 do begin trace(msg & " selected case 3"); end;
         when none  do begin trace(msg & " selected case none"); end;
      end case;
    end;
   

	if verbose then begin
		outtext ("--- START Simula a.s. TEST"); outint(nnn,4); outimage;
		outtext (title);
		outimage; outimage;
	end;

   facit(0):-"Test 1 selected case 2";
   facit(1):-"Test 2 selected case 1";
   facit(2):-"Test 3 selected case 3";
   facit(3):-"Test 4 selected case none";

   facit(4):-"Test 5 selected case 2";
   facit(5):-"Test 6 selected case 1";
   facit(6):-"Test 7 selected case 3";
   facit(7):-"Test 8 selected case none";

   facit(8):-"End of program";

   test1('B',"Test 1");
   test1('A',"Test 2");
   test1('C',"Test 3");
   test1('Z',"Test 4");

   test2(2,"Test 5");
   test2(1,"Test 6");
   test2(5,"Test 7");
   test2(27,"Test 8");

   trace("End of program");
      

	if noMessage then else begin
		if not found_error then begin
			outtext ("--- NO ERRORS FOUND IN TEST"); outint(nnn,4); outtext("  "); outtext(title); outimage;
		end;

		if verbose then begin
			outtext ("--- END Simula a.s. TEST"); outint(nnn,4); outimage;
		end;
	end;

end of program;
