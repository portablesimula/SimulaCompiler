
% ===============================================   Main   program =============
%  PROGRAM NAME:   Simula a.s. - SIMULA TEST NO 76
%
%  PURPOSE:     Test of detach and resume in SIMSET.
%
%  WRITTEN BY:  BEH   25/08-86
% ==============================================================================

begin
	integer nnn=76; text title="--- Test of detach and resume in simset.";
	
	boolean found_error;
	integer nFailed;
	boolean verbose=false;
	boolean noMessage;

	procedure err(t); text t;
	begin sysout.setpos(1);
	      outtext("*** error: in test "); outtext(t); outimage;
	      found_error:=true; nFailed:=nFailed+1;
	end;

   text t;

   procedure Print(t); value t; text t;
   begin outtext(t); end; 

   Simset begin
         ref(A) x;

         Link class A;
         begin Print("A");
               begin ref(C) Y;
                     class C;
                     begin Print("C"); detach;
                           Print("E");
                     end;

                     Print("B");
                     Y:- new C;
                     Print("D");
                     resume(Y);
                     Print("F");
               end;
               Print("G");
         end;

         Print("AA");
         x:- new A;
         Print("AB");

         t:- copy(sysout.image.strip);
         sysout.setpos(1);
         sysout.image:= notext;

         if t = "AAABCDEFGAB" then else
         begin err("detach - resume(1).");
               outtext("             Erroneus sequencing: ");
               outtext(t);
               outimage; outimage;
         end;
   end;

   Simset begin
         ref(A)x;

         Link class A;
         begin Print("A");
               detach;
               begin ref(C) Y;

                     class C;
                     begin Print("D"); detach;
                           Print("F"); this A.detach;
                           Print("H"); detach;
                           Print("J");
                     end;

                     Print("C"); Y:- new C;
                     Print("E"); resume(Y);
                     Print("I"); resume(Y);
                     detach
               end;
               Print("L");
          end;


	if verbose then begin
		outtext ("--- START Simula a.s. TEST"); outint(nnn,4); outimage;
		outtext (title);
		outimage; outimage;
	end;

          x:- new A;
          Print("B"); resume(x);
          Print("G"); resume(x);
          Print("K"); resume(x);
          Print("M");

          t:- copy(sysout.image.strip);
          sysout.setpos(1);
          sysout.image:= notext;

         if t = "ABCDEFGHIJKLM" then
         else
         begin err("*** error : detach - resume(2).");
                outtext("             Erroneus sequencing: ");
               outtext(t); outimage; outimage;
         end;
    end;
   

	if noMessage then else begin
		if not found_error then begin
			outtext ("--- NO ERRORS FOUND IN TEST"); outint(nnn,4); outtext("  "); outtext(title); outimage;
		end;

		if verbose then begin
			outtext ("--- END Simula a.s. TEST"); outint(nnn,4); outimage;
		end;
	end;

end of program;

