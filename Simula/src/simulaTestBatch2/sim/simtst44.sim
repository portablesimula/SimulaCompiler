
% ===============================================   Main   program =============
%  PROGRAM NAME:   Simula a.s. - SIMULA TEST NO 44
%
%  PURPOSE:     Test text procedure.
%
%  REWRITTEN BY:  BEH   24/07-86
% ==============================================================================

begin
	integer nnn=44; text title="--- Test text procedure.";
	
	boolean found_error;
	integer nFailed;
	boolean verbose=false;
	boolean noMessage;

	procedure err(t); text t;
	begin sysout.setpos(1);
	      outtext("*** error: in test "); outtext(t); outimage;
	      found_error:=true; nFailed:=nFailed+1;
	end;

   text               t1, t2;

   text procedure mysub(t, i1, i2); value t; text t; integer i1, i2;
   if  i2 < i1 or t.length < i2 then else begin
      text         th;
      integer      j;
      th :- blanks(i2 - i1 + 1);
      t.setpos(i1);
      for  j := i1  step 1 until i2 do  th.putchar(t.getchar);
      mysub :- th
   end of procedure Mysub;


	if verbose then begin
		outtext ("--- START Simula a.s. TEST"); outint(nnn,4); outimage;
		outtext (title);
		outimage; outimage;
	end;


   t1 :- copy("test one");
   t2 :- mysub(t1, 6, 8);
   t1 :- t1.sub(6, 3);
   if  t1 = t2  then else begin
      err("text values.");
      outimage;
      outtext("            Erroneus values :");
      outimage;
      outtext("              t1 == t2  -  ");
      outtext(if t1 == t2 then "true" else "false");
      outimage;
      outtext("              t1 == ");
      outtext(if t1 == notext then "notext" else t1);
      outimage;
      outtext("              t2 == ");
      outtext(if t2 == notext then "notext" else t2);
      outimage;
      outtext("              t1.main == ");
      outtext(if t1.main == notext then "notext" else t1.main);
      outimage;
      outtext("              t2.main == ");
      outtext(if t2.main == notext then "notext" else t2.main);
      outimage;
   end;


	if noMessage then else begin
		if not found_error then begin
			outtext ("--- NO ERRORS FOUND IN TEST"); outint(nnn,4); outtext("  "); outtext(title); outimage;
		end;

		if verbose then begin
			outtext ("--- END Simula a.s. TEST"); outint(nnn,4); outimage;
		end;
	end;

end of program;

