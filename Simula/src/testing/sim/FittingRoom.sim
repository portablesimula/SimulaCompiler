
% From: https://en.wikipedia.org/wiki/Simula

Simulation Begin

   Class FittingRoom; Begin
      Ref (Head) door;
      Boolean inUse;
      
      Procedure request; Begin
         If inUse Then Begin
             Wait (door);
             door.First.Out;
         End;
         inUse:= True;
      End;
      
      Procedure leave; Begin
         inUse:= False;
         reportFittingRoomAvailable;
         Activate door.First;
      End;
      
      procedure reportFittingRoomAvailable; Begin
         ref(Link) elt;
         OutFix (Time, 2, 6); OutText (": FittingRoom becomes available, Queue: ");
         elt:-door.first();
         if elt == none then outtext("Empty");
         while elt =/= none do Begin
            OutText(" " & (elt qua Person).pname);
            elt:-elt.suc();
         end;   
         OutImage;
      end;
      
      door:- New Head;
   End;
  
   Procedure report (message); Text message; Begin
      OutFix (Time, 2, 6); OutText (": " & message); OutImage;
   End;

   Process Class Person (pname); Text pname; Begin
      While True Do Begin
         Hold (Normal (12, 4, u));
         report  (pname & " is requesting the fitting room");
         fittingroom1.request;
         report (pname & " has entered the fitting room");
         Hold (Normal (3, 1, u));
         fittingroom1.leave;
         report (pname & " has left the fitting room");
      End;
   End;

   Integer u;
   Ref (FittingRoom) fittingRoom1;

   fittingRoom1:- New FittingRoom;
   Activate New Person ("Sam");
   Activate New Person ("Sally");
   Activate New Person ("Andy");
   Activate New Person ("Peter");
   Activate New Person ("Elisabeth");
   Activate New Person ("Harry");
   Activate New Person ("Robert");
   Activate New Person ("John");
   Hold (100);
End;