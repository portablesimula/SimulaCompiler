
% ===============================================   Main   program =============
%  PROGRAM NAME:   Simula a.s. - SIMULA TEST NO 52
%
%  PURPOSE:     Test 'inner'
%
%  WRITTEN BY:  BEH   31/07-86
% ==============================================================================
external class SimulaTest;

SimulaTest(52,"--- Test 'inner'") begin

   long real          a;
   real               b;

   class C;
   begin
      long real       d;
      d := a + b;
      inner;
      b := a + b + d
   end of class C;

   C class E;
   begin
      real            f;
      f := d + b;
      a := f + d
   end of class E;

   ref(C)             g;

   a := b := 5.45;

   g :- new C;

   if b-b/1000 > 21.8 or b+b/1000 < 21.8 or
      a-a/1000 > 5.45 or a+a/1000 < 5.45 then
   begin
      err("Global variables a or b have wrong values after execution of class body(1).");
      outtext("            Erroneus values : a = ");
      outreal(a, 4, 10); outimage;
      outtext("                              b = ");
      outreal(b, 4, 10); outimage;
   end;

   g :- new E;

   if b-b/1000 > 125.35 or b+b/1000 < 125.35 or
      a-a/1000 > 76.30  or a+a/1000 < 76.30  then
   begin
      err("Global variables a or b have wrong values after execution of class body(2).");
      outtext("            Erroneus values : a = ");
      outreal(a, 4, 10); outimage;
      outtext("                              b = ");
      outreal(b, 4, 10); outimage;
   end;

end;