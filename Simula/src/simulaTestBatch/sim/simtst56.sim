
% ===============================================   Main   program =============
%  PROGRAM name:   Simula a.s. - SIMULA TEST NO 56
%
%  PURPOSE:     Test virtual procedures.
%
%  WRITTEN BY:  BEH   01/08-86
% ==============================================================================
external class SimulaTest;

SimulaTest(56,"--- Test virtual procedures.") begin

   ref(A) x;   real ar, br;

   class A;
      virtual:   real procedure   Q;
   begin
      real procedure   Q;  Q:=2.5;

      procedure  T(R);
         name R;
         real R;
      begin
         ar:=R*Q
      end;
   end;

   procedure  S(P, B);
      name P, B;
      procedure P;
      real B;
   begin
      P(x.Q);
      br:=B*x.Q
   end;

   A class B;
   begin
      real  procedure  Q;   Q:=2;
   end;


   x:-new B;
   S(x.T, x.Q);

   if ar-ar/1000 > 4 or ar+ar/1000 < 4 or
      br-br/1000 > 4 or br+br/1000 < 4 then
   begin
      err("*** error : Wrong matching procedure called(1).");
      outimage;
      outtext("            Erroneus values :  ar = ");
      outfix(ar, 4, 8);
      outtext("   br = ");
      outfix(br, 4, 8);
      outimage;
   end;


   x:-new A;
   S(x.T, x.Q);

   if ar-ar/1000 > 6.25 or ar+ar/1000 < 6.25 or
      br-br/1000 > 6.25 or br+br/1000 < 6.25 then
   begin
      err("*** error : Wrong matching procedure called(2).");
      outimage;
      outtext("            Erroneus values :  ar = ");
      outfix(ar, 4, 8);
      outtext("   br = ");
      outfix(br, 4, 8);
      outimage;
   end;
   
 end
