
% ===============================================   Main   program =============
%  PROGRAM NAME:   Simula a.s. - SIMULA TEST NO 134
%
%  PURPOSE:     Test Outfile with CREATE, APPEND and PURGE.
%
%  WRITTEN BY:  MYH
% ==============================================================================
external class SimulaTest;

SimulaTest(134,"--- Test Outfile with CREATE, APPEND and PURGE.") begin

	integer i,byte;
	ref(Outfile) f1,f2;
	text expected;
	
	procedure trace(image); text image; begin
		sysout.outtext("Inimage: "); sysout.outtext(image); sysout.outimage;
	end;
	
	
%	outtext("===================  PASS 1  ================"); outimage;
	f1:-new Outfile("TEMP003");
	f1.setaccess("CREATE");
	f1.open(blanks(20));
	for i:=1 step 1 until 20 do begin
		f1.outtext("Pass 1, line "); f1.outint(i,0); 
		f1.outimage;
	end;
	f1.close;
	
	
%	outtext("===================  PASS 2  ================"); outimage;
	f2:-new Outfile("TEMP003");
	f2.setaccess("APPEND");
	f2.open(blanks(20));
	for i:=1 step 1 until 20 do begin
		f2.outtext("Pass 2, line "); f2.outint(i,0); 
		f2.outimage;
	end;
	f2.close;
	
%	outtext("===================  PASS 3  ================"); outimage;
	inspect new Infile("TEMP003") do begin
		open(blanks(20));
		for i:=1 step 1 until 20 do begin
			inimage;
%			sysout.outtext(image); sysout.outimage;
			if verbose then trace(image);
			expected :- "Pass 1, line " & edit(i);
			if image.strip ne expected then err(image & " ne " & expected);
		end;
		
		for i:=1 step 1 until 20 do begin
			inimage;
%			sysout.outtext(image); sysout.outimage;
			if verbose then trace(image);
			expected :- "Pass 2, line " & edit(i);
			if image.strip ne expected then err(image & " ne " & expected);
		end;
		close;
	end;		
	
end