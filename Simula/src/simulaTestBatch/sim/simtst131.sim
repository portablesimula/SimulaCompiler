% ===============================================   Main   program =============
%  PROGRAM NAME:   SIMULA TEST NO 131
%
%  PURPOSE: CATCHING ERRORS
%
%  WRITTEN BY:  MYH   2/9-2021
% ==============================================================================
CatchingErrors begin
    Boolean verbose=FALSE;
    Boolean found_error ;
    integer traceCase;
    text array facit(0:9);
    ref(InBytefile) bf;

    procedure onError(message); text message; begin
        trace("Inside onError: " & message);
        goto LAB2;
    end;
   
    procedure trace(msg); text msg;
    begin
        if verbose then begin outtext(msg); outimage; end;
        if msg NE facit(traceCase) then
        begin
            found_error:=TRUE;
            outtext("ERROR in Case "); outint(traceCase,0); outimage;
            outtext("Trace: " & msg); outimage;
            outtext("Facit: " & facit(traceCase)); outimage;
        end;
        traceCase:=traceCase+1;
    end;

    procedure P; begin
        procedure Q; begin
            integer i;
            
            CatchingErrors begin
                procedure onError(message); text message; begin
                    trace("DEEP Inside onError: " & message);
                    goto LAB4;
                end;
                bf.inbyte;
            end;
        end;
        Q;
    end;
   
   if verbose then begin
   Outtext("--- START SIMULA TEST 131"); Outimage ;
   Outtext("--- CATCHING ERRORS");
   Outimage; Outimage;
   end;
   
   facit(0):-"At START";
   facit(1):-"Inside onError: NullPointerException: NONE-CHECK Failed";
   facit(2):-"At LAB2";
   facit(3):-"DEEP Inside onError: NullPointerException: NONE-CHECK Failed";
   facit(4):-"At LAB4";
   facit(5):-"At END OF TEST";
   
STARTING: trace("At START");

    bf.inbyte; -- Producing error NONE CHECK FAILED
   
LAB1:trace("At LAB1");
LAB2:trace("At LAB2");

    P;

LAB3:trace("At LAB3");
LAB4:trace("At LAB4");
   
   trace("At END OF TEST");
      
   IF found_error THEN
   ELSE
   BEGIN
      Outtext ("--- NO ERRORS FOUND IN TEST 131");
   END ;

   if verbose then begin
      Outimage;
      Outtext ("--- END SIMULA TEST 131") ;
      Outimage;
   end;
   

 END
