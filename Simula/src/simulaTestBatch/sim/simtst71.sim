
% ===============================================   Main   program =============
%  PROGRAM NAME:   Simula a.s. - SIMULA TEST NO 71
%
%  PURPOSE:     Test visibility of identifiers.
%
%  WRITTEN BY:  BEH   22/08-86
% ==============================================================================
external class SimulaTest;

SimulaTest(71,"--- Test visibility of identifiers.") begin

   integer i;

   class A;
      virtual : procedure P1;
   begin
      procedure P1; i := 1;
   end;

   A class B;
   begin
      procedure P1; i := 2;
      procedure P2; i := 3;
   end;

   ref(A) rA;

   procedure P2; i := 4;

   rA :- new B;

   inspect rA do
   begin P2 end;

   if i = 4 then else begin
      err("Visibility of identifiers(1).");
      outtext("            Value of i : ");
      outint(i, 3); outimage; outimage;
   end;

   rA :- new B;

   inspect rA when B do
   begin P2 end;

   if i = 3 then else begin
      err("Visibility of identifiers(2).");
      outtext("            Value of i : ");
      outint(i, 3); outimage; outimage;
   end;


   rA :- new B;

   inspect rA do
   begin P1 end;

   if i = 2 then else begin
      err("Visibility of identifiers(3).");
      outtext("            Value of i : ");
      outint(i, 3); outimage; outimage;
   end;


   rA :- new A;

   inspect rA do
   begin P1 end;


   if i = 1 then else begin
      err("Visibility of identifiers(4).");
      outtext("            Value of i : ");
      outint(i, 3); outimage; outimage;
   end;

   
 end
