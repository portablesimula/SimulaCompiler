BEGIN  -- SYSTEM 1

   procedure trace(t); value t; text t; begin
      outtext(t); outimage;
   end trace;


   ref(Y) yy;  ref(X) xx;

   class Y; begin
      trace("new Y");
      detach;
      trace("resume Y");
      resume(xx);
      trace("terminate Y");
   end Y;

   class X; begin
      trace("new X");        detach;
      trace("resume X");

      begin -- SYSTEM  dvs.  BLOCK med lokale klasse(r)
         procedure B(E);  procedure E; begin
            trace("enter B");
            begin
               ref (C) cc;
               
               class C; begin
                  trace("new C");
                  detach;
                  trace("resume C");
                  E;
                  trace("terminate C");
               end C;
               
               cc :- new C;
               resume(cc);
            end;
            trace("exit B");
         end B;
         
         trace("enter A-block");
         
         begin -- D-BLOCK
         
            procedure E; begin
               integer i;
               trace("enter E");
               begin
                  ref(F) ff;
                  class F; begin
                     trace("new F");     detach;
                     trace("resume and exit F");
                  end F;
                  trace("Just before new F");  -- SISTE SOM SKRIVES
                  ff :- new F;
                  resume(ff);
               end;
               trace("exit E");
            end E;
            
            trace("enter D-block");
            
            B(E);
            resume(yy);
            trace("terminate D-block");
         end D;

         trace("terminate A-block"); end A;

      trace("terminate X");  end X;

   Outtext ("--- START Test complex use of detach and resume."); Outimage; Outimage;

   yy :- new Y;   xx :- new X;

   trace("resume(xx)"); resume(xx);

   
 END
