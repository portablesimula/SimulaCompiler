%
% Sieve of Eratosthenes
%
% In mathematics, the sieve of Eratosthenes is a simple, ancient algorithm
% for finding all prime numbers up to any given limit.
%
% See: https://en.wikipedia.org/wiki/Sieve_of_Eratosthenes
%
% This is an implementation of 28. juni 2000 by Karel Babcicky.
%

Simulation begin
        
   integer limit,acc; 
   integer startTime,timeUsed;
    
   Process class Prime; 
   begin integer which; 
         acc   := acc + 1; 
         which := time; 
         outint( which, 7 ); 
         while true do begin 
           if nextev.evtime > time + 1 then  
              activate new Prime delay 1; 
           hold(which); 
         end; 
   end; 
 
   outtext( "Show all primes less than " );
   breakoutimage; limit:=inint; 
 
   startTime:=cputime;
   
   activate new Prime at 2; 
   hold(limit); 
 
   outimage; outimage; 
   outint( acc, 7 ); outtext( " primes found" ); 
   
   outimage; outimage; 
   timeUsed := cputime - startTime;
   outtext("Elapsed Time: Approximately " & edit(timeUsed) & " sec.");
end