%
% Sieve of Eratosthenes
%
% In mathematics, the sieve of Eratosthenes is a simple, ancient algorithm
% for finding all prime numbers up to any given limit.
%
% See: https://en.wikipedia.org/wiki/Sieve_of_Eratosthenes
%
% This is an implementation of 28. juni 2000 by Karel Babcicky.
%

Simulation begin
        
   integer acc; 
   integer count;
    
   process class Prime; 
   begin integer which; 
         acc   := acc + 1; 
         which := time; 
         outint( which, 10 ); 
         while true do begin 
           if nextev.evtime > time + 1 then  
              activate new Prime delay 1; 
           hold(which); 
         end; 
   end; 
 
   count:=500000; --16000000;
   Outtext( "Show all primes less than " ); outint(count,0); Outimage;  
 
   activate new Prime at 2; 
   hold(count); 
 
   Outimage; Outimage; 
   Outint( acc, 10 ); Outtext( " primes found" );  
end



   1031130 primes found

End program: VirtualThreadError
 -  Memory(used=4280287232,free=1527680080)
 -  nProcessors=8
 -  VirtualThread Count = 1031131
 -  Elapsed Time Approximately 65993 sec.
END Execute .jar File. Exit value=0
