% ===============================================   Main   program =============
%  PROGRAM NAME:   SIMULA TEST NO 110
%
%  PURPOSE: Simple Co-Routine Sample 3: detach - resume - chain
%
%  WRITTEN BY:  MYH   28/04-2019
% ==============================================================================
external class SimulaTest;

SimulaTest(110,"--- Simple Co-Routine Sample 3: detach - resume - chain") begin
   integer traceCase;
   text array facit(0:11);
   ref(ProducerClass) producer;
   ref(ConsumerClass) consumer;
   
   procedure trace(msg); text msg;
   begin
      if verbose then begin outtext(msg); outimage; end;
      if msg <> facit(traceCase) then
      begin
         err("ERROR in Case " & edit(traceCase));
         outtext("Trace: " & msg); outimage;
         outtext("Facit: " & facit(traceCase)); outimage;
      end;
      traceCase:=traceCase+1;
   end;
   
   class ProducerClass;
   begin
	  trace("producer:Init");
	  detach;
	  trace("producer:Part 1 - Statements");
	  resume(consumer);
	  trace("producer:Part 2 - Statements");
	  resume(consumer);
	  trace("producer:Part 3 - Statements");
	  resume(consumer);
   end;
   
   class ConsumerClass;
   begin
	  trace("consumer:Init");
	  detach;
	  trace("consumer:Part 1 - Statements");
	  resume(producer);
	  trace("consumer:Part 2 - Statements");
	  resume(producer);
	  trace("consumer:Part 3 - Statements");
	  resume(producer);
   end;
   
   
   facit(0):-"Main: Before new producer";
   facit(1):-"producer:Init";
   facit(2):-"Main: Before new consumer";
   facit(3):-"consumer:Init";
   facit(4):-"Main: Before first resume";
   facit(5):-"producer:Part 1 - Statements";
   facit(6):-"consumer:Part 1 - Statements";
   facit(7):-"producer:Part 2 - Statements";
   facit(8):-"consumer:Part 2 - Statements";
   facit(9):-"producer:Part 3 - Statements";
   facit(10):-"consumer:Part 3 - Statements";
   facit(11):-"Main: End of Program";
   
   begin
      trace("Main: Before new producer");
      producer:-new ProducerClass;
      trace("Main: Before new consumer");
      consumer:-new ConsumerClass;
      trace("Main: Before first resume");
      resume(producer);
      trace("Main: End of Program");
   end subBlock;
   
end program;