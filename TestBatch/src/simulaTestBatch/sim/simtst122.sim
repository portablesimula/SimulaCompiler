% ===============================================   Main   program =============
%  PROGRAM NAME:   SIMULA TEST NO 122
%
%  PURPOSE: GOTO LABEL IN SUPER class
%
%  WRITTEN BY:  MYH   28/04-2019
% ==============================================================================
external class SimulaTest;

SimulaTest(122,"--- Test goto label in super class") begin
   integer traceCase;
   text array facit(0:9);
   integer n;
   
   procedure trace(msg); text msg;
   begin
      if verbose then begin outtext(msg); outimage; end;
      if msg <> facit(traceCase) then
      begin err("Case " & edit(traceCase));
         outtext("Trace: " & msg); outimage;
         outtext("Facit: " & facit(traceCase)); outimage;
      end;
      traceCase:=traceCase+1;
   end;

   class A;
   begin
      L1: trace("A.L1"); n:=n+1;
   end;

   A class B;
   begin
      L2: trace("B.L2"); n:=n+1;
      if n < 4 then goto L1;
   end;

   facit(0):-"A.L1";
   facit(1):-"B.L2";
   facit(2):-"A.L1";
   facit(3):-"B.L2";
   facit(4):-"At end OF TEST";
   
   new B;
   trace("At end OF TEST");
      
end;