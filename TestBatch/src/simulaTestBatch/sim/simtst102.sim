
% ===============================================   Main   program =============
%  PROGRAM NAME:   SIMULA TEST NO 102
%
%  PURPOSE: LEGAL GOTO out of an operating Process and the enclosing System.
%
%  WRITTEN BY:  MYH   17/03-2019
% ==============================================================================
external class SimulaTest;

SimulaTest(102,"--- LEGAL GOTO out of an operating process and the enclosing System.") begin

   integer traceCase;
   text array facit(0:5);
   
   procedure trace(line,msg); integer line; text msg;
   begin
      text case1;
      case1:-"LINE " & edit(line) & ": " & msg;
      if verbose then begin outtext(case1); outimage; end;
      if case1 <> facit(traceCase) then
      begin
         err("Trace Case " & edit(traceCase) & " at line " & edit(line));
         outtext("Trace Case: " & case1); outimage;
         outtext("     Facit: " & facit(traceCase)); outimage;
      end;
      traceCase:=traceCase+1;
   end;

   procedure p;
   begin
      integer i;
      Simulation begin
         ref(Car) car1;
      
         Process class Car(pname); text pname;
         begin
            trace(sourceline,pname & " is initiating");
            while true do
            begin
               trace(sourceline,pname & " is active 1");
               hold (4);
               trace(sourceline,pname & " is active 2");
               goto EXIT_LABEL;
		    end;
         end;
      
         trace(sourceline,"START SIMULATION");
         car1:- new Car("Bil 1");
         trace(sourceline,car1.pname & " is scheduled for 4.0");
         activate car1 at 4.0;
         hold (12);
      
      end;
   end;
      
   facit(0):-"LINE 48: START SIMULATION";
   facit(1):-"LINE 50: Bil 1 is scheduled for 4.0";
   facit(2):-"LINE 38: Bil 1 is initiating";
   facit(3):-"LINE 41: Bil 1 is active 1";
   facit(4):-"LINE 43: Bil 1 is active 2";
   facit(5):-"LINE 68: At OUT";

   p;
   
   outtext ("THIS LINE SHOULD NEVER BE EXECUTED"); outimage;
   
   EXIT_LABEL: trace(sourceline,"At OUT");

 end
