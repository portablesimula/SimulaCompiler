
% ===============================================   Main   program =============
%  PROGRAM NAME:   Simula a.s. - SIMULA TEST NO 136
%
%  PURPOSE:     Test Directfile with CREATE, APPEND and PURGE.
%
%  WRITTEN BY:  MYH
% ==============================================================================
external class SimulaTest;

SimulaTest(136,"--- Test Directfile with CREATE, APPEND and PURGE.") begin

	integer i;
	text expected;
	ref(Directfile) f1,f2;
	
	procedure trace(image); text image; begin
		sysout.outtext("Inimage: "); sysout.outtext(image); sysout.outimage;
	end;
	
	
%	outtext("===================  PASS 1  ================"); outimage;
	f1:-new Directfile("TEMP005");
	f1.setaccess("CREATE");
	f1.open(blanks(20));
	for i:=1 step 1 until 20 do begin
		f1.outtext("Pass 1, line "); f1.outint(i,0); 
		f1.outimage;
	end;
	f1.close;
	
	
%	outtext("===================  PASS 2  ================"); outimage;
	f2:-new Directfile("TEMP005");
	f2.setaccess("APPEND");
	f2.open(blanks(20));
	f2.locate(f2.lastloc+1);
	for i:=1 step 1 until 20 do begin
		f2.outtext("Pass 2, line "); f2.outint(i,0); 
		f2.outimage;
	end;
	
		
%	outtext("===================  PASS 3  ================"); outimage;
	inspect f2 do begin
		locate(1);
		for i:=1 step 1 until 20 do begin
			inimage;
%			sysout.outtext(image); sysout.outimage;
			if verbose then trace(image);
			expected :- "Pass 1, line " & edit(i);
			if image.strip ne expected then err(image & " ne " & expected);
		end;
		
		for i:=1 step 1 until 20 do begin
			inimage;
%			sysout.outtext(image); sysout.outimage;
			if verbose then trace(image);
			expected :- "Pass 2, line " & edit(i);
			if image.strip ne expected then err(image & " ne " & expected);
		end;

		locate(13); inimage;
		if verbose then trace(image);
		expected:-"Pass 1, line 13";
		if image.strip ne expected then err("Image(13) = " & image);


		CatchingErrors begin
			procedure onError(message); text message; begin
%				sysout.outtext("ERROR: " & message); sysout.outimage;
				text expected="RTS_SimulaRuntimeError: Directfile: outimage failed:"
							  " location(6) <= initial lastloc(20)"
				              " - The file TEMP005 was opend with APPEND";
%				sysout.outtext("ERR: " & message); sysout.outimage;
%				sysout.outtext("EXP: " & expected); sysout.outimage;
				if message ne expected then err("Got wrong message: " & message);
				goto E;
			end;
			locate(6); outtext("ERROR"); outimage;
			err("The program was never supposed to get here");
		end;
		
	 E: setaccess("PURGE");
		close;
	end;
	
end