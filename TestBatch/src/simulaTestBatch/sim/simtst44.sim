
% ===============================================   Main   program =============
%  PROGRAM NAME:   Simula a.s. - SIMULA TEST NO 44
%
%  PURPOSE:     Test text procedure.
%
%  REWRITTEN BY:  BEH   24/07-86
% ==============================================================================
external class SimulaTest;

SimulaTest(44,"--- Test text procedure.") begin

   text               t1, t2;

   text procedure mysub(t, i1, i2); value t; text t; integer i1, i2;
   if  i2 < i1 or t.length < i2 then else begin
      text         th;
      integer      j;
      th :- blanks(i2 - i1 + 1);
      t.setpos(i1);
      for  j := i1  step 1 until i2 do  th.putchar(t.getchar);
      mysub :- th
   end of procedure Mysub;

   t1 :- copy("test one");
   t2 :- mysub(t1, 6, 8);
   t1 :- t1.sub(6, 3);
   if  t1 = t2  then else begin
      err("text values.");
      outimage;
      outtext("            Erroneus values :");
      outimage;
      outtext("              t1 == t2  -  ");
      outtext(if t1 == t2 then "true" else "false");
      outimage;
      outtext("              t1 == ");
      outtext(if t1 == notext then "notext" else t1);
      outimage;
      outtext("              t2 == ");
      outtext(if t2 == notext then "notext" else t2);
      outimage;
      outtext("              t1.main == ");
      outtext(if t1.main == notext then "notext" else t1.main);
      outimage;
      outtext("              t2.main == ");
      outtext(if t2.main == notext then "notext" else t2.main);
      outimage;
   end;

end of program;
