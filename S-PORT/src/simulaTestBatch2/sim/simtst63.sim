
% ===============================================   Main   program =============
%  PROGRAM NAME:   Simula a.s. - SIMULA TEST NO 63
%
%  PURPOSE:     Test transmission by name of reference types.
%
%  WRITTEN BY:  BEH   06/08-86
% ==============================================================================

begin
	integer nnn=63; text title="--- Test transmission by name of reference types.";
	
	boolean found_error;
	integer nFailed;
	boolean verbose=false;
	boolean noMessage;

	procedure err(t); text t;
	begin sysout.setpos(1);
	      outtext("*** error: in test "); outtext(t); outimage;
	      found_error:=true; nFailed:=nFailed+1;
	end;

   class A;
   begin
      integer aa;
   end;

   A class B;
   begin
      integer bb;
   end;

   B class C;
   begin
      integer cc;
   end;

   ref(A) array X(0:2);
   integer i;

   procedure P(Q, x, i);
      name x, i;
%      procedure Q; ref(C) x; integer i;
      procedure Q; ref(A) x; integer i;
   begin
      Q(x, i);
   end;

   procedure Q(y, i);
      name y, i;
      ref(A) y; integer i;
   begin
      i := 0; y.aa := 1;
      i := 1; y qua B.bb := X(0).aa;
      i := 2; y qua C.cc := X(0).aa;
   end;


   procedure R(y, i);
      name y, i;
      ref(A) y; integer i;
   begin
      integer j;
%      for j := 0,1,2 do
      for j := 0 step 1 until 2 do
      begin
         i := j;
         inspect y
            when C do
               cc := X(0).aa
            when B do
               bb := X(0).aa
            when A do
               aa := 2;
      end;
   end;

   procedure S(y, i);
      name y, i;
      ref(A) y; integer i;
   begin
      integer j;
%      for j := 0,1,2 do
      for j := 0 step 1 until 2 do
      begin
         i := j;
         if y is C then
            y qua C.cc := X(0).aa
         else
         if y is B then
            y qua B.bb := X(0).aa
         else
         if y is A then
            y.aa := 3;
      end;
   end;


	if verbose then begin
		outtext ("--- START Simula a.s. TEST"); outint(nnn,4); outimage;
		outtext (title);
		outimage; outimage;
	end;


   X(0) :- new A;
   X(1) :- new B;
   X(2) :- new C;

   P(Q, X(i), i);

   if     X(0).aa = 1
      and X(1) qua B.bb = 1
      and X(2) qua C.cc = 1 then
   else
   begin
      err("Call by name(1).");
      outimage;
      outtext("            Erroneus values : X(0).aa = ");
      outint(X(0).aa, 4);
      outimage;
      outtext("                              X(1).bb = ");
      outint(X(1) qua B.bb, 4);
      outimage;
      outtext("                              X(2).cc = ");
      outint(X(2) qua C.cc, 4);
      outimage;
   end;

   P(R, X(i), i);

   if     X(0).aa = 2
      and X(1) qua B.bb = 2
      and X(2) qua C.cc = 2 then
   else
   begin
      err("Call by name(2).");
      outtext("            Erroneus values : X(0).aa = ");
      outint(X(0).aa, 4);
      outimage;
      outtext("                              X(1).bb = ");
      outint(X(1) qua B.bb, 4);
      outimage;
      outtext("                              X(2).cc = ");
      outint(X(2) qua C.cc, 4);
      outimage;
   end;

   P(S, X(i), i);

   if     X(0).aa = 3
      and X(1) qua B.bb = 3
      and X(2) qua C.cc = 3 then
   else
   begin
      err("Call by name(3).");
      outtext("            Erroneus values : X(0).aa = ");
      outint(X(0).aa, 4);
      outimage;
      outtext("                              X(1).bb = ");
      outint(X(1) qua B.bb, 4);
      outimage;
      outtext("                              X(2).cc = ");
      outint(X(2) qua C.cc, 4);
      outimage;
   end;


	if noMessage then else begin
		if not found_error then begin
			outtext ("--- NO ERRORS FOUND IN TEST"); outint(nnn,4); outtext("  "); outtext(title); outimage;
		end;

		if verbose then begin
			outtext ("--- END Simula a.s. TEST"); outint(nnn,4); outimage;
		end;
	end;

end of program;
