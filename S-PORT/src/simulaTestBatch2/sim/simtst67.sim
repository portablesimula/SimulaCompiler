
% ======================================   Main   program =============
%  PROGRAM NAME:   Simula a.s. - SIMULA TEST NO 67
%
%  PURPOSE:     Simple test of detach, call and resume.
%
%  WRITTEN BY:  BEH   08/08-86
% =====================================================================

begin
	integer nnn=67; text title="--- Simple test of detach, call and resume.";
	
	boolean found_error;
	integer nFailed;
	boolean verbose=false;
	boolean noMessage;

	procedure err(t); text t;
	begin sysout.setpos(1);
	      outtext("*** error: in test "); outtext(t); outimage;
	      found_error:=true; nFailed:=nFailed+1;
	end;


   integer array ia(1:10);
   integer i, nri;

   procedure savei(i);  integer i;
   begin  nri := nri + 1;   ia(nri) := i  end;

   procedure Checki;
   begin
      if     ia(1) = 2
         and ia(2) = 3
         and ia(3) = 5
         and ia(4) = 13
         and ia(5) = 18
         and ia(6) = 49
         and nri    = 6  then
      else
      begin
         integer j;
         err("Program sequencing.");
         outimage;
         outtext("            Subsequent values of i : ");
         outimage;
         for j := 1 step 1 until nri do
         begin
            outtext("            ");
            outint(j, 2);
            outtext(" : ");
            outint(ia(j), 4);
            outimage;
         end;
         outimage;
      end;
   end pros. checki;


   class A;
   begin integer j;
         ref(B) rb;

         j := i := i+i;  savei(i);     !(1) *** i=2 ***;
      detach;
         j := i := j+i;  savei(i);     !(3) *** i=5 ***;
      call(rb);
         j := i := j+i;  savei(i);     !(5) *** i=18 ***;
   end of class A;

   class B;
   begin integer j;

         j := i := i+1;   savei(i);    !(2) *** i=3 ***;
      detach;
         j := i := j+2*i; savei(i);    !(4) *** i=13 ***;
      detach;
         j := i := j+2*i; savei(i)     !(6) *** i=49 ***;
   end of class B;

   ref(A)            ua;
   integer            c;


	if verbose then begin
		outtext ("--- START Simula a.s. TEST"); outint(nnn,4); outimage;
		outtext (title);
		outimage; outimage;
	end;

   i := 1;
   ua :- new A;
   c := c + 1;
   ua.rb :- new B;
   resume(ua);
   resume(ua.rb);

   Checki;
   
 
	if noMessage then else begin
		if not found_error then begin
			outtext ("--- NO ERRORS FOUND IN TEST"); outint(nnn,4); outtext("  "); outtext(title); outimage;
		end;

		if verbose then begin
			outtext ("--- END Simula a.s. TEST"); outint(nnn,4); outimage;
		end;
	end;

end of program;
 
