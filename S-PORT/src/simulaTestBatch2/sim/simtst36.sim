
% =====================================   Main   program =============
%  PROGRAM name:   Simula a.s. - SIMULA TEST NO 36
%
%  PURPOSE:     Test Jensens Device - Call by name.
%
%  REWRITTEN BY:  BEH   23/07-86
% ====================================================================

begin
	integer nnn=36; text title="--- Test Jensens Device - Call by name.";
	
	boolean found_error;
	integer nFailed;
	boolean verbose=false;
	boolean noMessage;

	procedure err(t); text t;
	begin sysout.setpos(1);
	      outtext("*** error: in test "); outtext(t); outimage;
	      found_error:=true; nFailed:=nFailed+1;
	end;

   real eps;

   procedure Gps(n, v, z);  name v, z;  integer n;  real v, z;
   begin
%    integer cv;
      for  cv:= 1  step 1 until n do  v:= z;
   end;

   procedure test(r, v); real r, v;
   begin
      if r-r*eps > v or r+r*eps < v then
      begin
         err("Erroneous value: ");
         outreal(r, 5, 12);
         outimage;
      end;
   end;

   integer array      x(1:9);
   integer            cv, limit;
   real               innerp, simpson;


	if verbose then begin
		outtext ("--- START Simula a.s. TEST"); outint(nnn,4); outimage;
		outtext (title);
		outimage; outimage;
	end;


   eps:= 0.001;

   for  cv:= 1  step 1 until 9 do x(cv):= cv*cv*cv;
   limit:= 9;
   
   Gps(limit, innerp, innerp + x(cv) !**2; *x(cv));

                         comment WE HAVE NOW FOUND
                                 1**6+2**6+3**6...9**6=978405
                                 HELD IN INNERP;
   test(innerp, 978405);

   limit:= 4;
   Gps(limit, simpson, simpson +(x(2*cv - 1) + 4*x(2*cv) + x(2*cv + 1))/3);
   comment SIMPSON NOW HOLDS 1640 THE INTEGRAL OF ***3 BETWEEN 1 AND 9
           FOR FORTHER EXAMPLES OF A MORE GENERAL GPS WHICH WILL COMPUTE
           ANY COMPUTABLE FUNCTION SEE KNUTH AND MERNER CACM V4, (1961)
           PP. 268-272 "ALGOL 60 CONFIDENTIAL".
           THERE IS AN error P.271, LINE 24 WHERE THE LAST CALL ON GPS
           SHOULD READ:  "Gps(A, 1 0, I, -1.0)";

   test(simpson, 1640);


	if noMessage then else begin
		if not found_error then begin
			outtext ("--- NO ERRORS FOUND IN TEST"); outint(nnn,4); outtext("  "); outtext(title); outimage;
		end;

		if verbose then begin
			outtext ("--- END Simula a.s. TEST"); outint(nnn,4); outimage;
		end;
	end;

end of program;
