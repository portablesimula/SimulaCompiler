
begin
	integer nnn=54; text title="--- A VERY Simple Simulation";
	
	boolean found_error;
	integer nFailed;
	boolean verbose=false;
	boolean noMessage;

	procedure err(t); text t;
	begin sysout.setpos(1);
	      outtext("*** error: in test "); outtext(t); outimage;
	      found_error:=true; nFailed:=nFailed+1;
	end;

	if verbose then begin
		outtext ("--- START Simula a.s. TEST"); outint(nnn,4); outimage;
		outtext (title);
		outimage; outimage;
	end;


Simulation begin   
   integer u;
   ref(Car) bil1,bil2;
   
   integer testCase;
   text array facit(0:33);
  
   procedure initFacit; begin
      facit(0):-"0.00: START Simtest1";
      facit(1):-"0.00: Bil 1 is scheduled for 4.0";
      facit(2):-"0.00: Bil 2 is scheduled for 6.0";
      facit(3):-"4.00: Bil 1 is initiating";
      facit(4):-"4.00: Bil 1 is active 1";
      facit(5):-"6.00: Bil 2 is initiating";
      facit(6):-"6.00: Bil 2 is active 1";
      facit(7):-"8.00: Bil 1 is active 2";
      facit(8):-"8.00: Bil 1 is active 1";
      facit(9):-"10.00: Bil 2 is active 2";
      facit(10):-"10.00: Bil 2 is active 1";
      facit(11):-"12.00: Final END of Simulation";
   end;
   
   procedure report(message); text message; begin
      text T; T:-blanks(40);
      outfix(time, 2, 0); outtext(": " & message);
      T:=sysout.image.strip; sysout.image:=notext; sysout.image.setpos(1);
      if T.strip <> facit(testCase) then err(T.strip & " <> " & facit(testCase));
      testCase:=testCase+1;
   end;
 
   Process class Car(pname); text pname; begin
      report(pname & " is initiating");
      while true do begin
         report(pname & " is active 1");
         hold(4);
         report(pname & " is active 2");
	  end;
   end;

	if verbose then begin
		outtext ("--- START Simula a.s. TEST"); outint(nnn,4); outimage;
		outtext (title);
		outimage; outimage;
	end;


   initFacit;

   report("START Simtest1");
   bil1:- new Car("Bil 1");
   bil2:- new Car("Bil 2");
   report(bil1.pname & " is scheduled for 4.0");
   activate bil1 at 4.0;
   report(bil2.pname & " is scheduled for 6.0");
   activate bil2 at 6.0;
   hold(12);
   
end;
   

	if noMessage then else begin
		if not found_error then begin
			outtext ("--- NO ERRORS FOUND IN TEST"); outint(nnn,4); outtext("  "); outtext(title); outimage;
		end;

		if verbose then begin
			outtext ("--- END Simula a.s. TEST"); outint(nnn,4); outimage;
		end;
	end;

end of program;
   