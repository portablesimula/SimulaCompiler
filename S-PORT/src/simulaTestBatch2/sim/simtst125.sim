% ===============================================   Main   program =============
%  PROGRAM NAME:   SIMULA TEST NO 125
%
%  PURPOSE: GOTO LABEL WITHIN NESTED COMPOUND STATEMENTS
%
%  WRITTEN BY:  MYH   28/04-2019
% ==============================================================================

begin
	integer nnn=125; text title="--- Test goto label within compound statements";
	
	boolean found_error;
	integer nFailed;
	boolean verbose=false;
	boolean noMessage;
   
   text procedure edint(i); integer i;
   begin text t; t:-blanks(6); t.putint(i); edint:-t; end;

	procedure err(t); text t;
	begin sysout.setpos(1);
	      outtext("*** error: in test "); outtext(t); outimage;
	      found_error:=true; nFailed:=nFailed+1;
	end;

   integer traceCase;
   text array facit(0:3);
   integer n;
   
   procedure trace(msg); text msg;
   begin
      if verbose then begin outtext(msg); outimage; end;
      if msg <> facit(traceCase) then
      begin err("Case " & edint(traceCase));
            outtext("Trace: " & msg); outimage;
            outtext("Facit: " & facit(traceCase)); outimage;
      end;
      traceCase:=traceCase+1;
   end;


	if verbose then begin
		outtext ("--- START Simula a.s. TEST"); outint(nnn,4); outimage;
		outtext (title);
		outimage; outimage;
	end;

   facit(0):-"BEGIN PROGRAM";
   facit(1):-"At L3";
   facit(2):-"At L1";
   facit(3):-"END PROGRAM";
   
   trace("BEGIN PROGRAM");
   
   goto L3;
   
   begin
      begin
         begin
            L3: trace("At L3");
         end;
         goto L1;
         L2: trace("At L2");
      end;
      L1: trace("At L1");
   end;
   
   trace("END PROGRAM");


	if noMessage then else begin
		if not found_error then begin
			outtext ("--- NO ERRORS FOUND IN TEST"); outint(nnn,4); outtext("  "); outtext(title); outimage;
		end;

		if verbose then begin
			outtext ("--- END Simula a.s. TEST"); outint(nnn,4); outimage;
		end;
	end;

end of program;
