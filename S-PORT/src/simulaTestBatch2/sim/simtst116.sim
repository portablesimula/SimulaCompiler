% ===============================================   Main   program =============
%  PROGRAM NAME:   SIMULA TEST NO 116
%
%  PURPOSE: Simple QPS-Sample 2
%
%  WRITTEN BY:  MYH   28/04-2019
% ==============================================================================

begin
	integer nnn=116; text title="--- Simple QPS-Sample 2";
	
	boolean found_error;
	integer nFailed;
	boolean verbose=false;
	boolean noMessage;
   
   text procedure edint(i); integer i;
   begin text t; t:-blanks(6); t.putint(i); edint:-t; end;

	procedure err(t); text t;
	begin sysout.setpos(1);
	      outtext("*** error: in test "); outtext(t); outimage;
	      found_error:=true; nFailed:=nFailed+1;
	end;

   integer traceCase;
   text array facit(0:8);
   ref(C)  x;
   
   procedure trace(msg); text msg;
   begin
      if verbose then begin outtext(msg); outimage; end;
      if msg <> facit(traceCase) then begin
         err("Case " & edint(traceCase));
         outtext("Trace: " & msg); outimage;
         outtext("Facit: " & facit(traceCase)); outimage;
      end;
      traceCase:=traceCase+1;
   end;

   procedure P;
   begin
      x:-new C;
      ! - - - ;
      trace("In Procedure P - Just Before first Resume(x)");
      resume(x);
   end;

   class C;
   begin
      procedure Q;
      begin
         ! - - - ;
         trace("In Procedure Q - Just Before first Detach");
         detach;
         ! - - - ;
         trace("In Procedure Q - Just Before second Detach");
         detach;
         ! - - - ;
      end;
      ! - - - ;
      Q;
      ! - - - ;
   end;
   ! - - - ;
   

	if verbose then begin
		outtext ("--- START Simula a.s. TEST"); outint(nnn,4); outimage;
		outtext (title);
		outimage; outimage;
	end;
   
   facit(0):-"In Block - Just Before call procedure P";
   facit(1):-"In Procedure Q - Just Before first Detach";
   facit(2):-"In Procedure P - Just Before first Resume(x)";
   facit(3):-"In Procedure Q - Just Before second Detach";
   facit(4):-"In Block - Just Before final END";
   
   
   trace("In Block - Just Before call procedure P");
   P;
   ! - - - ;
   trace("In Block - Just Before final END");
   
   

	if noMessage then else begin
		if not found_error then begin
			outtext ("--- NO ERRORS FOUND IN TEST"); outint(nnn,4); outtext("  "); outtext(title); outimage;
		end;

		if verbose then begin
			outtext ("--- END Simula a.s. TEST"); outint(nnn,4); outimage;
		end;
	end;

end of program;
