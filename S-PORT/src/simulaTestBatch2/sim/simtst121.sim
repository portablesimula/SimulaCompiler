% ----------------------------------------------------------------
% LEGAL GOTO out of an operating Process and the enclosing System.
% ----------------------------------------------------------------
% ===============================================   Main   program =============
%  PROGRAM NAME:   SIMULA TEST NO 121
%
%  PURPOSE: LEGAL GOTO out of an operating Process and the enclosing System.
%
%  WRITTEN BY:  MYH   28/04-2019
% ==============================================================================

begin
	integer nnn=121; text title="--- Test LEGAL GOTO out of an operating Process and the enclosing System.";
	
	boolean found_error;
	integer nFailed;
	boolean verbose=false;
	boolean noMessage;
   
   text procedure edint(i); integer i;
   begin text t; t:-blanks(6); t.putint(i); edint:-t; end;

	procedure err(t); text t;
	begin sysout.setpos(1);
	      outtext("*** error: in test "); outtext(t); outimage;
	      found_error:=true; nFailed:=nFailed+1;
	end;

   integer traceCase;
   text array facit(0:9);
   
   procedure trace(msg); text msg;
   begin
      if verbose then begin outtext(msg); outimage; end;
      if msg <> facit(traceCase) then
      begin err("Case " & edint(traceCase));
         outtext("Trace: " & msg); outimage;
         outtext("Facit: " & facit(traceCase)); outimage;
      end;
      traceCase:=traceCase+1;
   end;


   procedure p;
   begin
      integer i;
      Simulation begin
         ref(Car) car1;
      
         Process class Car(pname); text pname;
         begin
            trace(pname & " is initiating");
            while true do
            begin
               trace(pname & " is active 1");
               hold (4);
               trace(pname & " is active 2");
               goto EXIT_LABEL;
		    end;
         end;
      
         trace("START SIMULATION");
         car1:- new Car("Bil 1");
         trace(car1.pname & " is scheduled for 4.0");
         activate car1 at 4.0;
         hold (12);
      
      end;
   end;
   


	if verbose then begin
		outtext ("--- START Simula a.s. TEST"); outint(nnn,4); outimage;
		outtext (title);
		outimage; outimage;
	end;

   facit(0):-"--- START GOTO SAMPLE";
   facit(1):-"START SIMULATION";
   facit(2):-"Bil 1 is scheduled for 4.0";
   facit(3):-"Bil 1 is initiating";
   facit(4):-"Bil 1 is active 1";
   facit(5):-"Bil 1 is active 2";
   facit(6):-"At END OF TEST";

   trace("--- START GOTO SAMPLE");
   p;
   trace("THIS LINE SHOULD NEVER BE EXECUTED");
   EXIT_LABEL: trace("At END OF TEST");
      

	if noMessage then else begin
		if not found_error then begin
			outtext ("--- NO ERRORS FOUND IN TEST"); outint(nnn,4); outtext("  "); outtext(title); outimage;
		end;

		if verbose then begin
			outtext ("--- END Simula a.s. TEST"); outint(nnn,4); outimage;
		end;
	end;

end of program;
