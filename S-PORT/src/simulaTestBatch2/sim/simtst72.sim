
% ===============================================   Main   program =============
%  PROGRAM name:   Simula a.s. - SIMULA TEST NO 72
%
%  PURPOSE:     Test of formal procedures.
%
%  WRITTEN BY:  BEH   25/08-86
% ==============================================================================

begin
	integer nnn=72; text title="--- Test of formal procedures.";
	
	boolean found_error;
	integer nFailed;
	boolean verbose=false;
	boolean noMessage;

	procedure err(t); text t;
	begin sysout.setpos(1);
	      outtext("*** error: in test "); outtext(t); outimage;
	      found_error:=true; nFailed:=nFailed+1;
	end;

   
   integer nA;
   ref(A) array rA(0:20);
   ref(A) x, v;
   
   procedure TEST(msg); text msg;
   begin
      if verbose then
      begin
         outtext("TEST: " & msg);
         if v =/= none then
         begin outtext(" -- v.i ="); outint(v.i,2);
         end;
         outimage; LIST;
       end;  
   end; 
   
   procedure LIST;
   begin
      integer j;
      for j:=0 step 1 until nA-1 do
      begin
         outtext("Object "); outint(j,0);
         if rA(j) =/= none then
         begin outtext(" -- i="); outint(rA(j).i,0); end
         else outtext(" -- NULL");
         outimage;
      end;
   end;   

   class A;
   begin
         
      ref(A) procedure Z;
      begin P(x);
            Z:- v:- new A;
      end;

      integer  i;
      rA(nA):-this A; nA:=nA+1;
      i:=5;
      if verbose then
      begin
         outtext("new A("); outint(nA-1,0);
         outtext(").i=5, nA="); outint(nA,0); outimage;
      end;   
   end;

   procedure P(y); name y; ref(A)y;
   begin
       Q(y.i)
   end;

   procedure Q(ii); name ii; integer ii;
   begin
      TEST("BEFORE ii:=ii+2");
      ii:=ii+2;
      ! The above assigment is compiled to: ;
      !    tmp:=ii+2; ii:=tmp;            ;
      ! Which is very WRONG for NAME params ;
      TEST("AFTER ii:=ii+2");
   end;



	if verbose then begin
		outtext ("--- START Simula a.s. TEST"); outint(nnn,4); outimage;
		outtext (title);
		outimage; outimage;
	end;

   x:- new A;
   P(x.Z);


   if x.i = 9 and v.i = 7 then else begin
      err("procedure calls.");
      outimage;
      outtext("            Erroneus values: x.i = ");
      if x =/= none then
         outint(x.i, 5)
      else
         outtext(" object none");
      outimage;
      outtext("                             v.i = ");
      if v =/= none then
         outint(v.i, 5)
      else
         outtext(" object none");
      outimage;
      outimage
   end;
   

	if noMessage then else begin
		if not found_error then begin
			outtext ("--- NO ERRORS FOUND IN TEST"); outint(nnn,4); outtext("  "); outtext(title); outimage;
		end;

		if verbose then begin
			outtext ("--- END Simula a.s. TEST"); outint(nnn,4); outimage;
		end;
	end;

end of program;

