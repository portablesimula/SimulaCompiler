
% =====================================   Main   program =============
%  PROGRAM NAME:   SIMULA A/S - SIMULA TEST NO 26
%
%  PURPOSE:     Test designational expressions, the goto statement
%               and switch declaration.
%
%  Flow:        t0 -> l1 -> t1 -> l2 -> t2 -> l3.
%
%  WRITTEN BY:  BEH   18/07-86
%  MODIFIED BY: MYH   24/07-2018
% ====================================================================
BEGIN
   Boolean found_error;
   Boolean verbose=FALSE;

   integer i, j, t;
   Boolean b,BBB;
   
   procedure trace(msg); Text msg;
   begin
      if verbose then
      begin outtext(msg & " - t,i,j,b ="); 
         Outint(t, 3); Outint(i, 3); Outint(j, 3);
         Outtext(if b then " true" else " false");
         Outimage;
      end;   
   end;

   if verbose then begin
   Outtext("--- START Simula a.s. TEST 26"); Outimage;
   end;

   i := 5;

   begin
      switch st := t1, t2;
%      switch s := l1, l2, q(i), if b imp j > i then q(2) else l1;
      switch s := l1, l2, q(i), if b imp j > i then q(2) else l2;
      switch q := s(1), q(4), q(2),
                  if if j=i then not b else b eqv b then L3 else s(2), q(1);
                  
                  
      Text procedure Pst(ix); integer ix;
      begin  if ix=1 then Pst:-"T1"
        else if ix=2 then Pst:-"T2";
      end; 
       
      Text procedure Ps(ix); integer ix;
      begin  if ix=1 then Ps:-"L1"
        else if ix=2 then Ps:-"L2"
        else if ix=3 then Ps:-Pq(i)
%        else if ix=4 then Ps:- if b imp j > i then Pq(2) else "L1"; 
        else if ix=4 then Ps:- if b imp j > i then Pq(2) else "L2"; 
      end;  
      
      Text procedure Pq(ix); integer ix;
      begin  if ix=1 then Pq:-Ps(1)
        else if ix=2 then Pq:-Pq(4)
        else if ix=3 then Pq:-Pq(2)
        else if ix=4 then
        begin 
           Outtext("IN TEXT PROCEDURE Pq: t,i,j,b=");
           Outint(t, 3); Outint(i, 3); Outint(j, 3);
           Outtext(if b then " true" else " false"); outimage;
           
           Outtext("if j=i then not b else b eqv b EVALUTES "); 
           BBB:=if j=i then not b else b eqv b;
           Outtext(if BBB then " true" else " false"); outimage;
           
           Outtext("if j=i then not b else (b eqv b) EVALUTES "); 
           BBB:=if j=i then not b else (b eqv b);
           Outtext(if BBB then " true" else " false"); outimage;
           
           Outtext("(b eqv b) EVALUTES "); 
           BBB:=(b eqv b);
           Outtext(if BBB then " true" else " false"); outimage;
           
           Outtext("if if j=i then not b else b eqv b then L3 else Ps(2) EVALUTES "); 
           Outtext(if if j=i then not b else b eqv b then "L3" else Ps(2)); outimage;

           Pq:-if if j=i then not b else b eqv b then "L3" else Ps(2)
        end
        else if i=5 then Pq:-Pq(1);
      end;

  T0: trace("AT T0 -- Should GOTO L1");
      if t <> 0 or i <> 5 or j <> 0 or b then
      begin
         Outtext("*** error: Evaluation of designational expression,");
         Outimage;
         Outtext("           goto statement or program sequence ");
         Outtext           ("control (1)."); 
         Outimage;
         Outtext("           Value of significant variables :");
         Outimage;
         Outtext("           ");
         Outint(t, 3); Outint(i, 3); Outint(j, 3);
         Outtext(if b then " true" else " false");
         Outimage;
         found_error := true;
      end;

      t := 1;
      trace("Should GOTO " & Pq(i));
      go to q(i);


  T1: trace("AT T1 -- Should GOTO L2");
   if t <> 1 then
      begin
         Outtext("*** error: Evaluation of designational expression,");
         Outimage;
         Outtext("           goto statement or program sequence ");
         Outtext           ("control (2).");
         Outimage;
         Outtext("           Value of significant variables :");
         Outimage;
         Outtext("           ");
         Outint(t, 3); Outint(i, 3); Outint(j, 3);
         Outtext(if b then " true" else " false");
         Outimage;
         found_error := true;
      end;

      t := 2;
      j := 5;
      b:=true;
      trace("Just before GOTO L2"); 
      trace("Should GOTO " & Ps(4));
      goto s(4);

  T2: trace("AT T2 -- Should GOTO L3");
   if t <> 2 or i <> 5 or j <> 5 or not b then
      begin
         Outtext("*** error: Evaluation of designational expression,");
         Outimage;
         Outtext("           goto statement or program sequence ");
         Outtext           ("control (3).");
         Outimage;
         Outtext("           Value of significant variables :");
         Outimage;
         Outtext("           ");
         Outint(t, 3); Outint(i, 3); Outint(j, 3);
         Outtext(if b then " true" else " false");
         Outimage;
         found_error := true;
      end;

      t := j := i := 3; b:=false;
      trace("Just before GOTO L3"); 
      
      goto  s(3);


  L1: trace("AT L1 -- Should GOTO T1");
  
   if t <> 1 or i <> 5 or j <> 0 or b then
      begin
         Outtext("*** error: Evaluation of designational expression,");
         Outimage;
         Outtext("           goto statement or program sequence ");
         Outtext           ("control (4).");
         Outimage;
         Outtext("           Value of significant variables :");
         Outimage;
         Outtext("           ");
         Outint(t, 3); Outint(i, 3); Outint(j, 3);
         Outtext(if b then " true" else " false");
         Outimage;
         found_error := true;
      end;

      begin switch r := s(1), q(4), s(3);
         begin character c;
            trace("Should GOTO " & Pst(t));
            goto st(t)
         end;
      end;

  L2: trace("AT L2 -- Should GOTO T2");
%   if t <> 2 or i <> 5 or j <> 5 or b then
   if t <> 2 or i <> 5 or j <> 5 or not b then
      begin
         Outtext("*** error: Evaluation of designational expression,");
         Outimage;
         Outtext("           goto statement or program sequence ");
         Outtext           ("control (5).");
         Outimage;
         Outtext("           Value of significant variables :");
         Outimage;
         Outtext("           ");
         Outint(t, 3); Outint(i, 3); Outint(j, 3);
         Outtext(if b then " true" else " false");
         Outimage;
         found_error := true;
      end;

      b := true;
      goto st(t);


  L3: trace("AT L3 -- Should TERMINATE");
%   if t <> 3 or i <> 3 or j <> 3 or not b then
   if t <> 3 or i <> 3 or j <> 3 or b then
      begin
         Outtext("*** error: Evaluation of designational expression,");
         Outimage;
         Outtext("           goto statement or program sequence ");
         Outtext           ("control (6).");
         Outimage;
         Outtext("           Value of significant variables :");
         Outimage;
         Outtext("           ");
         Outint(t, 3); Outint(i, 3); Outint(j, 3);
         Outtext(if b then " true" else " false");
         Outimage;
         found_error := true;
      end;
   end;


   IF found_error THEN
   ELSE
   BEGIN
      Outtext("--- NO ERRORS FOUND IN TEST 26");
   END;

   if verbose then begin
   Outtext("--- END Simula a.s. TEST 26");
   Outimage;
   end;
   
 END;
