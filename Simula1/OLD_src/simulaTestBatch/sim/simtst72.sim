
% ===============================================   Main   program =============
%  PROGRAM Name:   Simula a.s. - SIMULA TEST NO 72
%
%  PURPOSE:     Test of formal procedures.
%
%  WRITTEN BY:  BEH   25/08-86
% ==============================================================================
Begin
   boolean verbose=FALSE;
   
   integer nA;
   ref(A) array rA(0:20);
   Ref(A) x, v;
   
   Procedure TEST(msg); text msg;
   begin
      if verbose then
      begin
         outtext("TEST: " & msg);
         if v =/= none then
         begin outtext(" -- v.i ="); outint(v.i,2);
         end;
         outimage; LIST;
       end;  
   end; 
   
   Procedure LIST;
   begin
      integer j;
      for j:=0 step 1 until nA-1 do
      begin
         Outtext("Object "); outint(j,0);
         if rA(j) =/= none then
         begin outtext(" -- i="); outint(rA(j).i,0); end
         else outtext(" -- NULL");
         outimage;
      end;
   end;   

   Class A;
   Begin
         
      Ref(A) Procedure Z;
      Begin P(x);
            Z:- v:- New A;
      End;

      Integer  i;
      rA(nA):-this A; nA:=nA+1;
      i:=5;
      if verbose then
      begin
         outtext("NEW A("); outint(nA-1,0);
         outtext(").i=5, nA="); outint(nA,0); outimage;
      end;   
   End;

   Procedure P(y); Name y; Ref(A)y;
   Begin
       Q(y.i)
   End;

   Procedure Q(ii); Name ii; Integer ii;
   Begin
      TEST("BEFORE ii:=ii+2");
      ii:=ii+2
      TEST("AFTER ii:=ii+2");
   End;

   if verbose then begin
   Outtext("--- START Simula a.s. TEST 72") ;
   Outimage ;
   Outtext("--- Test of formal procedures.") ;
   Outimage ; Outimage ;
   end;

   x:- New A;
   P(x.Z);


   IF x.i = 9 and v.i = 7 THEN
   Begin
      Outtext("--- NO ERRORS FOUND IN TEST 72") ;
   End Else
   Begin
      Outtext ("*** error : Procedure calls.") ;
      Outimage ;
      Outtext ("            Erroneus values: x.i = ") ;
      if x =/= none then
         Outint (x.i, 5)
      else
         Outtext (" object none") ;
      Outimage ;
      Outtext ("                             v.i = ") ;
      if v =/= none then
         Outint (v.i, 5)
      else
         Outtext (" object none") ;
      Outimage ;
      Outimage
   End;

   if verbose then begin
   Outtext("--- END Simula a.s. TEST 72") ;
   Outimage
   end
   
 End
