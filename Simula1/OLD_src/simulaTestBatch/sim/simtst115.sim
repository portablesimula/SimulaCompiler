% ===============================================   Main   program =============
%  PROGRAM NAME:   SIMULA TEST NO 115
%
%  PURPOSE: Simple QPS-Sample 1
%
%  WRITTEN BY:  MYH   28/04-2019
% ==============================================================================
begin
   Boolean verbose=FALSE;
   Boolean found_error ;
   integer traceCase;
   text array facit(0:8);
   ref(C)  x;
   
   procedure trace(msg); text msg;
   begin
      if verbose then begin outtext(msg); outimage; end;
      if msg NE facit(traceCase) then
      begin
         found_error:=TRUE;
         outtext("ERROR in Case "); outint(traceCase,0); outimage;
         outtext("Trace: " & msg); outimage;
         outtext("Facit: " & facit(traceCase)); outimage;
      end;
      traceCase:=traceCase+1;
   end;

   class A;
   begin
      x:-new C();
      ! - - - ;
      trace("In Class A - Just Before first Resume(x)");
      resume(x);
   end;

   class C;
   begin
      procedure Q;
      begin
         ! - - - ;
         trace("In Procedure Q - Just Before first Detach");
         detach;
         ! - - - ;
         trace("In Procedure Q - Just Before second Detach");
         detach;
         ! - - - ;
      end;
      ! - - - ;
      Q;
      ! - - - ;
   end;
   ! - - - ;
   
   
   if verbose then begin
   Outtext("--- START SIMULA TEST 115"); Outimage ;
   Outtext("--- Simple QPS-Sample 1");
   Outimage; Outimage;
   end;
   
   facit(0):-"In Block - Just Before create first A"; 
   facit(1):-"In Procedure Q - Just Before first Detach"; 
   facit(2):-"In Class A - Just Before first Resume(x)"; 
   facit(3):-"In Procedure Q - Just Before second Detach"; 
   facit(4):-"In Block - Just Before create second A"; 
   facit(5):-"In Procedure Q - Just Before first Detach"; 
   facit(6):-"In Class A - Just Before first Resume(x)"; 
   facit(7):-"In Procedure Q - Just Before second Detach"; 
   facit(8):-"In Block - Just Before final END"; 
    
   trace("In Block - Just Before create first A");
   new A;
   trace("In Block - Just Before create second A");
   new A;
   trace("In Block - Just Before final END");
   ! - - - ;
   
      
   IF found_error THEN
   ELSE
   BEGIN
      Outtext ("--- NO ERRORS FOUND IN TEST 115");
   END ;

   if verbose then begin
      Outimage;
      Outtext ("--- END SIMULA TEST 115") ;
      Outimage;
   end;
   
end;