%
% Sieve of Eratosthenes
%
% In mathematics, the sieve of Eratosthenes is a simple, ancient algorithm
% for finding all prime numbers up to any given limit.
%
% See: https://en.wikipedia.org/wiki/Sieve_of_Eratosthenes
%
% This is an implementation of 28. juni 2000 by Karel Babcicky.
%

Simulation begin
        
   integer acc; 
    
   process class Prime; 
   begin integer which; 
         acc   := acc + 1; 
         which := time; 
         outint( which, 7 ); 
         while true do begin 
           if nextev.evtime > time + 1 then  
              activate new Prime delay 1; 
           hold(which); 
         end; 
   end; 
 
   Outtext( "Show all primes less than " ); BreakOutimage;  
 
   activate new Prime at 2; 
   hold(inint); 
 
   Outimage; Outimage; 
   Outint( acc, 7 ); Outtext( " primes found" );  
end
