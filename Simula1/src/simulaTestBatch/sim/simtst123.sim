% ===============================================   Main   program =============
%  PROGRAM NAME:   SIMULA TEST NO 123
%
%  PURPOSE: GOTO VIRTUAL LABEL
%
%  WRITTEN BY:  MYH   28/04-2019
% ==============================================================================
begin
   Boolean verbose=FALSE;
   Boolean found_error ;
   integer traceCase;
   text array facit(0:9);
   integer n;
   
   procedure trace(msg); text msg;
   begin
      if verbose then begin outtext(msg); outimage; end;
      if msg NE facit(traceCase) then
      begin
         found_error:=TRUE;
         outtext("ERROR in Case "); outint(traceCase,0); outimage;
         outtext("Trace: " & msg); outimage;
         outtext("Facit: " & facit(traceCase)); outimage;
      end;
      traceCase:=traceCase+1;
   end;


   Class A; virtual: Label L1; Procedure P;
   BEGIN
      Switch S1 := L1, L3, L6;

      L1: trace("A.L1"); n:=n+1;
      
      L3: trace("A.L3");
      L6: trace("A.L6");
   END;

   A Class B;
   BEGIN
      Procedure P; BEGIN END;
      BEGIN_B: trace("BEGIN B");
      L1: trace("B.L1"); n:=n+1;
     
      if n < 4 then
      BEGIN
         trace("JUST BEFORE GOTO L1 - B.L1");
         goto L1;
      END;
   END;
   

   if verbose then begin
      Outtext("--- START SIMULA TEST 123"); Outimage ;
      Outtext("--- GOTO VIRTUAL LABEL");
      Outimage; Outimage;
   end;
   
   facit(0):-"A.L1";
   facit(1):-"A.L3";
   facit(2):-"A.L6";
   facit(3):-"BEGIN B";
   facit(4):-"B.L1";
   facit(5):-"JUST BEFORE GOTO L1 - B.L1";
   facit(6):-"B.L1";
   facit(7):-"JUST BEFORE GOTO L1 - B.L1";
   facit(8):-"B.L1";
   facit(9):-"At END OF TEST";
   
   new B;
   trace("At END OF TEST");
      
   IF found_error THEN
   ELSE
   BEGIN
      Outtext ("--- NO ERRORS FOUND IN TEST 123");
   END ;

   if verbose then begin
      Outimage;
      Outtext ("--- END SIMULA TEST 123") ;
      Outimage;
   end;
   
END;   
