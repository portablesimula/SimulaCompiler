% ===============================================   Main   program =============
%  PROGRAM NAME:   SIMULA TEST NO 117
%
%  PURPOSE: Simple QPS-Sample 3
%
%  WRITTEN BY:  MYH   28/04-2019
% ==============================================================================
begin
   Boolean verbose=FALSE;
   Boolean found_error ;
   integer traceCase;
   text array facit(0:9);
   Ref (A) x,y;
   
   procedure trace(msg); text msg;
   begin
      if verbose then begin outtext(msg); outimage; end;
      if msg NE facit(traceCase) then
      begin
         found_error:=TRUE;
         outtext("ERROR in Case "); outint(traceCase,0); outimage;
         outtext("Trace: " & msg); outimage;
         outtext("Facit: " & facit(traceCase)); outimage;
      end;
      traceCase:=traceCase+1;
   end;

   Class A(id); text id;
   Begin trace("Event A(" & id & ")-1: Just Before First Detach");
         Detach;
         trace("Event A(" & id & ")-2: Just Before Second Detach");
         Detach;
         if(y=/=none) then
         begin
            trace("Event A(" & id & ")-3: Just Before Resume(y)");
            resume(y);
         end;
         trace("Event A(" & id & ")-4: Just Before Final-END");
   End;


   
   if verbose then begin
   Outtext("--- START SIMULA TEST 117"); Outimage ;
   Outtext("--- Simple QPS-Sample 3");
   Outimage; Outimage;
   end;
   
   facit(0):-"Event 1: Just Before x:-New A";
   facit(1):-"Event A(x)-1: Just Before First Detach";
   facit(2):-"Event 2: Just Before y:-New A";
   facit(3):-"Event A(y)-1: Just Before First Detach";
   facit(4):-"Event 3: Just Before first Resume(x)";
   facit(5):-"Event A(x)-2: Just Before Second Detach";
   facit(6):-"Event 4: Just Before second Resume(x)";
   facit(7):-"Event A(x)-3: Just Before Resume(y)";
   facit(8):-"Event A(y)-2: Just Before Second Detach";
   facit(9):-"Event x: Just Before final END";


   trace("Event 1: Just Before x:-New A");
   x:- New A("x");
   trace("Event 2: Just Before y:-New A");
   y:- New A("y");
   trace("Event 3: Just Before first Resume(x)");
   Resume(x);
   trace("Event 4: Just Before second Resume(x)");
   Resume(x);
   trace("Event x: Just Before final END");
   
      
   IF found_error THEN
   ELSE
   BEGIN
      Outtext ("--- NO ERRORS FOUND IN TEST 117");
   END ;

   if verbose then begin
      Outimage;
      Outtext ("--- END SIMULA TEST 117") ;
      Outimage;
   end;
   

end;