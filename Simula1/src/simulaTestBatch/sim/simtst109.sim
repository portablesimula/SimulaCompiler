% ===============================================   Main   program =============
%  PROGRAM NAME:   SIMULA TEST NO 109
%
%  PURPOSE: Simple Co-Routine Sample 2: detach - resume
%
%  WRITTEN BY:  MYH   28/04-2019
% ==============================================================================
begin
   Boolean verbose=FALSE;
   Boolean found_error ;
   integer traceCase;
   text array facit(0:6);
   ref(A) x;
   
   procedure trace(msg); text msg;
   begin
      if verbose then begin outtext(msg); outimage; end;
      if msg NE facit(traceCase) then
      begin
         found_error:=TRUE;
         outtext("ERROR in Case "); outint(traceCase,0); outimage;
         outtext("Trace: " & msg); outimage;
         outtext("Facit: " & facit(traceCase)); outimage;
      end;
      traceCase:=traceCase+1;
   end;
   
   class A;
   begin
      integer i;
      trace("A: State 1(Initiating)");
      detach;     
      trace("A: State 2");
      detach;
      trace("A: State 3(Terminating)");
   end A;
   
   
   if verbose then begin
   Outtext("--- START SIMULA TEST 109"); Outimage ;
   Outtext("--- Simple Co-Routine Sample");
   Outimage; Outimage;
   end;
   
   facit(0):-"Main: Before new A";
   facit(1):-"A: State 1(Initiating)";
   facit(2):-"Main: Before first resume";
   facit(3):-"A: State 2";
   facit(4):-"Main: Before second resume";
   facit(5):-"A: State 3(Terminating)";
   facit(6):-"Main: Before third resume";
   
   begin
      trace("Main: Before new A");
      x:-new A();
      trace("Main: Before first resume");
      resume(x);
      trace("Main: Before second resume");
      resume(x);
      trace("Main: Before third resume");
      !resume(x); !Will cause ERROR: Resume(x): x is not in detached state.
   end subBlock;
   
   IF found_error THEN
   ELSE
   BEGIN
      Outtext ("--- NO ERRORS FOUND IN TEST 109");
      !Outimage;
   END ;

   if verbose then begin
   Outtext ("--- END SIMULA TEST 109") ;
   Outimage;
   end;
   
end program;