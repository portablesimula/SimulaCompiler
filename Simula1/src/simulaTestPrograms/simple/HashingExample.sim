%
% HashingExample from page 25 in Simula Common Base
%
% The following class expresses a notion of "hashing",
% in which the "hash" algorithm itself is a "replaceable part".
% "error" is a suitable non-local procedure.
%
begin
   class hashing(n); integer n; virtual: integer procedure hash;
   begin integer procedure hash(T); value T; text T;
         begin integer i;
                 L: if T.more then
                     begin i := i + rank(T.getchar);
                           goto L;
                     end;
                     hash := i - ( i / n * n );
         end hash;
         text array table(0:n-1); integer entries;
         integer procedure lookup(T,old); name old; value T; Boolean old; text T;
         begin integer i;
               i := hash(T);
            L: if table(i) == notext then
               begin table(i) :- T; entries := entries+1; end
               else if table(i) = T then old := true
               else if entries = n then error("hash table filled completely")
               else
               begin i := i+1;
                     if i = n then i := 0;
                     goto L
               end;
               lookup := i;           
         end lookup;
   end hashing;  
        
   hashing class ALGOL_hash;
   begin integer procedure hash(T); value T; text T;
         begin integer i; character c;
           L:  if T.more then
               begin c := T.getchar;
                    if c ne ' ' then i := i + rank(c);
                    goto L;
               end;
               hash := i - ( i / n * n );
         end hash;
   end ALGOL hash;
                                    
end program;                            
