
% ===============================================   Main   program =============
%  PROGRAM NAME:   Simula a.s. - SIMULA TEST NO 78
%
%  PURPOSE:     Test the text procedure filename of class file.
%
%  DEPENDS ON:  Random drawing.
%
%  NOTE:    1 - At lines marked with "NOTE 1", the characters which are allowed
%               in a filename(except letters and digit) should be filled in.
%
%           2 - At line marked with "NOTE 2" the maximum length of a filename
%               should be filled in.
%
%  WRITTEN BY:  BEH   27/08-86
% ==============================================================================
external class SimulaTest;

SimulaTest(78,"--- Test the text procedure filename of class file.") begin

   character array C(1:45);

   integer i, maxlength, u;

   procedure Test;
   begin integer i; ref(File) xf; text filnavn;

         filnavn:- blanks(maxlength);
         i:= randint(1,26,u);
         while i<=maxlength and i>0 do
         begin filnavn.putchar(C(i));
               i:= randint(-5,45,u);
         end;

         if i<2  then xf:- new Infile(filnavn) else
         if i<9  then xf:- new Outfile(filnavn) else
         if i<16 then xf:- new Directfile(filnavn) else
         if i<23 then xf:- new Inbytefile(filnavn) else
         if i<30 then xf:- new Outbytefile(filnavn) else
         if i<37 then xf:- new Directbytefile(filnavn) else
                      xf:- new Printfile(filnavn);

         if xf.filename <> filnavn then
         begin err("Attribute filename of class file.");
               outtext("           Value of actual parameter and attribute:");
               outimage;
               outtext("             "); outtext(filnavn);
               outtext("  "); outtext(xf.filename); outimage;
         end;
   end pros. Test;



   C(1):= 'A'; C(2):= 'B'; C(3):= 'C';
   C(4):= 'D'; C(5):= 'E'; C(6):= 'F';
   C(7):= 'G'; C(8):= 'H'; C(9):= 'I';
   C(10):= 'J'; C(11):= 'K'; C(12):= 'L';
   C(13):= 'M'; C(14):= 'N'; C(15):= 'O';
   C(16):= 'P'; C(17):= 'Q'; C(18):= 'R';
   C(19):= 'S'; C(20):= 'T'; C(21):= 'U';
   C(22):= 'V'; C(23):= 'W'; C(24):= 'X';
   C(25):= 'Y'; C(26):= 'Z'; C(27):= '0';
   C(28):= '1'; C(29):= '2'; C(30):= '3';
   C(31):= '4'; C(32):= '5'; C(33):= '6';
   C(34):= '7'; C(35):= '8'; C(36):= '9';
   C(37):= '-';                                 !**** NOTE 1 ****;
   C(38):= 'B';                                 !**** NOTE 1 ****;
   C(39):= 'C';                                 !**** NOTE 1 ****;
   C(40):= 'D';                                 !**** NOTE 1 ****;
   C(41):= 'E';                                 !**** NOTE 1 ****;
   C(42):= 'F';                                 !**** NOTE 1 ****;
   C(43):= 'G';                                 !**** NOTE 1 ****;
   C(44):= 'H';                                 !**** NOTE 1 ****;
   C(45):= 'I';                                 !**** NOTE 1 ****;

   maxlength:= 20;                              !**** NOTE 2 ****;
   u:= 54323;

   for i:= 1 step 1 until 50 do Test;

   
 end
