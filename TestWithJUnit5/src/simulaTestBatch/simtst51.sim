
% ===============================================   Main   program =============
%  PROGRAM NAME:   Simula a.s. - SIMULA TEST NO 51
%
%  PURPOSE:     Test 'inner'.
%
%  WRITTEN BY:  BEH   31/07-86
% ==============================================================================
external class SimulaTest;

SimulaTest(51,"--- Test 'inner'") begin

   class A;
   begin
      outtext(" L1 ");
      inner;
      outtext(" L2 ")
   end of class A;

   class D;
      outtext(" L3 ");

   A class B;
      outtext(" L4 ");

   B class F;
   begin
      outtext(" L5 ");
      inner
   end of class F;

   A class C;
   begin
      outtext(" L6 ");
      inner;
      outtext(" L7 ")
   end of class C;

   C class G;
      outtext(" L8 ");

   C class J;
   begin
      outtext(" L9 ");
      inner;
      outtext(" L10 ")
   end of class J;

   B class I;
   begin
      outtext(" L11 ");
      inner;
      outtext(" L12 ")
   end of class I;

   D class E;
   begin
      outtext(" L13 ");
      inner;
      outtext(" L14 ")
   end of class E;

   E class H;
   begin
      outtext(" L15 ");
      inner;
      outtext(" L16 ")
   end of class H;

   D class K;
   begin
      outtext(" L17 ");
      inner;
      outtext(" L18 ")
   end of class K;

   L class N;
   begin
      outtext(" L21 ");
      inner;
      outtext(" L22 ")
   end of class N;

   K class M;
      outtext(" L19 ");

   D class L;
      outtext(" L20 ");


   ref(A) ra;
   ref(D) rd;

   procedure Check(t, vt, errnr);
      value t, vt;
      text t, vt;
      integer errnr;
   begin
      sysout.image.setpos(1);
      sysout.image := notext;

      if t = vt then
      else
      begin
         err("Sequence of execution of statements in ");
         outtext("            class bodies(");
         outint(errnr, if errnr < 10 then 1 else 2);
         outtext(").");
         outimage;
         outtext("            Erroneus value :");
         outimage;
         outtext("                  ");
         outtext(t);
         outimage;
         outtext("            Should have been :");
         outimage;
         outtext("                  ");
         outtext(vt);
         outimage; outimage;
      end;
   end  pros. check;

   ra :- new A;
   Check(sysout.image.strip, " L1  L2", 1);

   rd :- new D;
   Check(sysout.image.strip, " L3", 2);

   ra :- new B;
   Check(sysout.image.strip, " L1  L4  L2", 3);

   ra :- new C;
   Check(sysout.image.strip, " L1  L6  L7  L2", 4);

   rd :- new E;
   Check(sysout.image.strip, " L3  L13  L14", 5);

   ra :- new F;
   Check(sysout.image.strip, " L1  L4  L5  L2", 6);

   ra :- new G;
   Check(sysout.image.strip, " L1  L6  L8  L7  L2", 7);

   ra :- new I;
   Check(sysout.image.strip, " L1  L4  L11  L12  L2", 8);

   ra :- new J;
   Check(sysout.image.strip, " L1  L6  L9  L10  L7  L2", 9);

   rd :- new H;
   Check(sysout.image.strip, " L3  L13  L15  L16  L14", 10);

   rd :- new M;
   Check(sysout.image.strip, " L3  L17  L19  L18", 11);

   rd :- new N;
   Check(sysout.image.strip, " L3  L20  L21  L22", 12);

   rd :- new L;
   Check(sysout.image.strip, " L3  L20", 13);

   rd :- new K;
   Check(sysout.image.strip, " L3  L17  L18", 14);

end;