
% ===============================================   Main   program =============
%  PROGRAM NAME:   Simula a.s. - SIMULA TEST NO 61
%
%  PURPOSE:     Test remote access to attributes which are protected.
%
%  WRITTEN BY:  BEH   04/08-86
% ==============================================================================
external class SimulaTest;

SimulaTest(61,"--- Test remote access to attributes which are protected.") begin

   class A;
   begin
      integer i;
      integer procedure vai; vai := i;
   end;

   A class B;
      protected i;
   begin
      integer i;

      integer procedure vbi; vbi := i;

      procedure p; x.i := 1;
   end;

   ref(B) x;

   x :- new B;
   x.p;      ! i.e. B'i := 1;
   x.i := 2; ! i.e. A'i := 2;

   if x.vai = 2 and x.vbi = 1 then
   else
   begin
      err("Visibility of protected attribute(1).");
      outtext("            Erroneus values : x.vai = ");
      outint(x.vai, 4); outimage;
      outtext("                              x.vbi = ");
      outint(x.vbi, 4); outimage;
   end;

   B begin
      x.i := 3;
      i := 4;

      if x.vbi = 3 and vbi = 4 then
      else
      begin
         err("*** error : Visibility of protected attribute(2).");
         outtext("             Erroneus values : x.vbi = ");
         outint(x.vbi, 4); outimage;
         outtext("                                 vbi = ");
         outint(vbi, 4); outimage;
      end;
   end;

 end;