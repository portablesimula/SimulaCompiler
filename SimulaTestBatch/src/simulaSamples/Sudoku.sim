Simset begin ref(Head) SQchain; 
  ref(SQ) array SQinst( 1 : 9, 1 : 9 ); 
   
  Link class SQexit(success); boolean success; 
  begin integer i, j; 
    detach; 
    if success then begin 
      outtext("PUZZLE SOLUTION:"); outimage; 
      for i := 1 step 1 until 9 do begin 
        for j := 1 step 1 until 9 do  
          outint( SQinst( i, j ).val, 2 ); 
        outimage; 
      end; 
      outimage; 
      outtext( "WE DID IT!" ); 
    end 
    else outtext( "The puzzle has no solution!" ); 
  end; 
 
  Link class SQ( row, col, val ); integer row, col, val;  
  begin 
    integer next_val; 
     
    boolean procedure legit(val); integer val; 
    begin 
      integer i, j, reg_row, reg_col; 
 
      for i := 1 step 1 until 9 do 
         if SQinst( row, i ).val = val then goto exit; 
      for i := 1 step 1 until 9 do 
         if SQinst( i, col ).val = val then goto exit; 
      ! 3x3 regions ; 
      reg_row := row - mod( row - 1, 3 ); 
      reg_col := col - mod( col - 1, 3 ); 
      for i := reg_row step 1 until reg_row + 2 do   
      for j := reg_col step 1 until reg_col + 2 do  
         if SQinst( i, j ).val = val then goto exit;  
      legit := true; 
exit:   
    end; 
  
    outint( val, 2 ); 
    if val = 0 then 
       into(SQchain); 
    detach; 
     
    while true do begin 
      for next_val := 1 step 1 until 9 do 
        if legit(next_val) then begin 
           val := next_val; 
           resume(suc); 
        end; 
      val := 0; 
      resume(pred) ! retracing; 
    end;  
  end; 
   
  procedure get_input; 
  begin  
    integer i, j; 
    
    outtext( "PUZZLE INPUT:" ); outimage; 
    inspect new InFile( "data/SudokuDifficult.txt" ) do begin 
      open( blanks(80) ); 
      for i := 1 step 1 until 9 do begin  
        for j := 1 step 1 until 9 do 
           SQinst( i, j ) :- new SQ( i, j, inint ); 
        outimage; 
      end;  
      close; 
    end; 
    outimage; 
  end; 
      
  SQchain :- new Head; 
  new SQexit(false).into(SQchain); 
  get_input; 
  new SQexit(true).into(SQchain); 
  resume( SQchain.first.suc ); 
end
