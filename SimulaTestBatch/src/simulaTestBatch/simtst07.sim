
% ==========================================   Main   program   ================
%  PROGRAM NAME:  Simula a.s. - SIMULA TEST NO 7
%
%  PURPOSE :  To test the mathematical library (x**y, Sqrt, ln, exp, sin, cos
%             and arctan) on variables of type real.
%
%  METHOD :   Compare the results obtained by using the functions in different
%             ways, which should give the same answer.  if the difference is
%             unacceptable, an error-message is given.
%
%  NOTE:  1 - At line marked with NOTE 1, the lowest acceptable difference
%             between the answers should be filled in.
%
%  COMMENTED BY:  BEH  08/07-86
%  pje 24/8 87: changed NOTE 1 from 1&-11 (absurd for reals!!) to 5&-7
% ==============================================================================
external class SimulaTest;

SimulaTest(7,"--- Test of math. functions") begin

class Testmatlib ;
   begin

   boolean  found_error;
   boolean verbose=false;

   procedure report (t) ;  value t ;  text t ;
      begin
	      err("*** Error in the " & t & " routine");
      end report ;

   boolean procedure realdiff (x, y) ;  real x, y ;
      begin
      real     diff ;
      diff := abs (if  x = 0  then  x - y  else  (x - y)/x) ;

      if  diff > 5.0&-05   then                    !***** NOTE 1 *****;
         begin
         outtext ("*** rel. Error =") ;
         outreal (diff, 15, 22) ;
         outimage ;
         realdiff := true
      end;
   end of procedure realdiff ;


   real procedure Rpower (x, y) ;  real x, y ;
      Rpower := exp (y*ln (x)) ;


   real procedure Ipower (b, j) ;  real b ;  integer j ;
      begin
      integer  exp ;
      real     prod, base ;

      boolean procedure Bit0 (n) ;  integer  n ;
         Bit0 := (n//2)*2 < n ;

      base := b ;
      exp :=  abs (j) ;
      prod := if Bit0 (exp) then base else 1.0 ;

      while  exp > 1  do
         begin
         base := base*base ;
         exp := exp//2 ;
         if Bit0 (exp) then prod := prod*base
         end ;

      Ipower := if j>0 then prod else 1.0/prod
      end of procedure Ipower ;

   procedure Testrpower (b, e) ;  real b, e ;
      begin
      real     x, y, z, r ;
      r := b ;
      x := Rpower (b, e) ;
      y := b**e ; z := r**e ;

      if realdiff (x, y) or realdiff (y, z)
      then
         begin
         report ("Rpower") ;
         outtext ("  base=") ;
         outreal (r, 15, 22) ;
         outtext ("  exp=") ;
         outreal (e, 15, 22) ;
         outimage ;
         outtext ("  base**exp=") ;
         outreal (x, 15, 22) ;
         outreal (y, 15, 22) ;
         outreal (z, 15, 22) ;
         outimage ;
         outimage ;
         outimage
         end

      end of procedure Testrpower ;

   procedure Testipower (b, e) ;  integer  b, e ;
      begin
      real    x, y, z, r ;
      r := b ;
      x := Ipower (b, e) ;
      y := b**e ;
      z := r**e ;

      if realdiff (x, y) or realdiff (y, z)
      then
         begin
         report ("Ipower") ;
         outtext ("  base=") ;
         outreal (r, 15, 22) ;
         outtext ("  exp=") ;
         outreal (e, 15, 22) ;
         outimage ;
         outtext ("  base**exp=") ;
         outreal (x, 15, 22) ;
         outreal (y, 15, 22) ;
         outreal (z, 15, 22) ;
         outimage ;
         outimage ;
         outimage
         end

      end of procedure Testipower ;


   procedure Testsqrt (x) ;  real  x ;
      begin
      real     a ;
      a :=  sqrt (x)**2 ;

      if realdiff (a, x)
      then
         begin
         report ("sqrt") ;
         outtext ("   x=") ;
         outreal (x, 15, 22) ;
         outtext (" SQRT (x)**2=") ;
         outreal (a, 15, 22) ;
         outimage ;
         outimage ;
         outimage
         end

      end of procedure Testsqrt ;

   procedure Testln (x, y) ;  real  x, y ;
      begin
      real    a, b ;
      a := ln (x*y) ;
      b := ln (x)+ln (y) ;

      if realdiff (a, b)
      then
         begin
         report ("ln") ;
         outtext ("   x, y=") ;
         outreal (x, 15, 22) ;
         outreal (y, 15, 22) ;
         outimage ;
         outtext ("   ln (x*y)=") ;
         outreal (a, 15, 22) ;
         outtext ("   ln (x)+ln (y)=") ;
         outreal (b, 15, 22) ;
         outimage ;
         outimage ;
         outimage
         end

      end of procedure Testln ;

   procedure Testexp (x, y) ;  real  x, y ;
      begin
      real    a, b ;
      a := exp (x + y) ;
      b := exp (x)*exp (y) ;

      if  realdiff (a, b)
      then
         begin
         report ("exp") ;
         outtext ("   x, y=") ;
         outreal (x, 15, 22) ;
         outreal (y, 15, 22) ;
         outimage ;
         outtext ("   exp (x+y)=") ;
         outreal (a, 15, 22) ;
         outtext ("   exp (x)*exp (y)=") ;
         outreal (b, 15, 22) ;
         outimage ;
         outimage ;
         outimage
         end

      end of procedure Testexp ;

   procedure Testlnexp (x) ;  real  x ;
      begin
      real    a, b ;
      a := exp (ln (x)) ;
      b := ln (exp (x)) ;

      if realdiff (a, b) or realdiff (b, x)
      then
         begin
         report ("ln or exp") ;
         outtext ("   x=") ;
         outreal (x, 15, 22) ;
         outimage ;
         outtext ("  exp (ln (x))=") ;
         outreal (a, 15, 22) ;
         outtext ("  ln (exp (x))=") ;
         outreal (b, 15, 22) ;
         outimage ;
         outimage ;
         outimage
         end

      end of procedure Testlnexp ;


   procedure Testsincos (x) ;  real  x ;
      begin
      real    a ;
      a := sin (x)**2 + cos (x)**2 ;

      if realdiff (a, 1)
      then
         begin
         report ("sin or cos") ;
         outtext ("   x=") ;
         outreal (x, 15, 22) ;
         outtext ("   sin (x)**2+ cos (x)**2=") ;
         outreal (a, 15, 22) ;
         outimage ;
         outimage ;
         outimage
         end

      end of procedure Testsincos ;

   procedure Testarctan (x) ;  real  x ;
      begin
      real    a ;
      a := arctan (sin (x)/cos (x)) ;

      if realdiff (a, x)
      then
         begin
         report ("arctan") ;
         outtext ("   x=") ;
         outreal (x, 15, 22) ;
         outtext ("   arctan (sin (x)/cos (x))=") ;
         outreal (a, 15, 22) ;
         outimage ;
         outimage ;
         outimage
         end

      end of procedure Testarctan ;

      procedure testepsilon(x); long real x;
      begin
         long real a, b;
         a:= subepsilon(addepsilon(x));
         b:= addepsilon(subepsilon(x));
         if realdiff(a,x) or realdiff (b,x) then
         begin
            report("Add/sub-epsilon");
            outtext("   x=");
            outreal(x,15,22); outimage;
         end;
      end procedure testepsilon;
   
end of class Testmatlib ;


Testmatlib begin
           integer  i, j, k, u1, u2 ;
           real     x, y ; real explim;
           integer Ipowblim= 215,
                   Ipowelim=4;
           real    powblim=1.1&9,
                   powelim=4.0;
           real    Twopi=6.2831853,
                   Pihalf=1.5707963;
           real    hlexp;
           u1 := 455470315;             ! Start values chosen to avoid;
           u2 := 2011455689;            ! dependent series;
           explim:= ln(maxreal);

           for  k := 1  step  1  until  500  do
              begin
              i := randint (-Ipowblim, Ipowblim, u1) ;
              j := randint ( 0, Ipowelim, u2) ;
              if i<>0 and j<>0 then Testipower (i, j) ;
              x := uniform (0.0, powblim, u1) ;
              y := uniform (-powelim, powelim, u2) ;
              Testrpower (x, y) ;
              x := uniform (0.0, maxreal, u1) ;
              Testsqrt (x) ;
              y := uniform (0.0, maxreal/x, u1) ;
              Testln (x, y) ;
              x := uniform (-explim, explim, u1) ;
              y:= abs(x);
              hlexp:= if y<1 then explim else explim/y;
              y := uniform (-hlexp, hlexp, u1) ;
              Testexp (x/2, y/2) ;
              Testlnexp (abs (x)) ;
              x := uniform (-Twopi, Twopi, u1) ;
              Testsincos (x) ;
              x := uniform (-Pihalf, Pihalf, u1) ;
              Testarctan (x);
              testepsilon(x);
              end;

           testepsilon(0.0);
           end;

end of program;
