
% ***************************************************************************
% NOTE: COPIED FROM: PC-SIMULA-BACKUP/Peter Backup Part2/disk7/pje/prog
% ***************************************************************************
begin text Anna,upAnna; ! the name ;
      integer U1; ! left operand seed;
      integer U2; ! right operand seed;
      integer U3; ! random response seed;

      integer lefthi, righthi,rightlo, left, right, ires, startR;
      integer quest,answer,correct;
      text opertext,t,helpoper; character operch;

      integer okresLim = 10, noReslim = 8;
      text array okRes(0:okReslim), noRes(0:noReslim);

      text procedure BRAVO;
      begin BRAVO:-okRes(randint(startR,okReslim,U3));
            correct:=correct+startR; startR:=1; end;
      
      text procedure NONO;
      begin NONO :-noRes(randint(startR,noReslim,U3)); startR:=0; end;
      
      boolean procedure okResult(left,right,ires);
              integer left,right,ires;
      begin
            okResult:=if operch='*' then ires=left*right
                 else if operch='-' then ires=left-right
                 else if operch='+' then ires=left+right
                 else if operch='/' then ires=left/right
                 else if operch='!' then ires=left**right
                 else false;
      end;

      procedure getImage(t); text t;
      inspect sysin do begin image:=notext;
              while image.strip=notext do begin
                    outtext(t); breakoutimage; inimage end;
      end;

      integer procedure getNumber(prompt); text prompt;
      begin boolean ok; character first;
            ok:=false;
            while not ok do inspect sysin do begin
                  getimage(prompt);
                  first:=inchar;
                  if first='q' or first='Q' then goto STOPIT;
                  setpos(pos-1);
                  while more and then not digit(inchar) do;
                  if not more then begin
                     outline("Oops! - that's not a number - TRY AGAIN");
                     outimage end
                  else begin
                     if pos>2 then begin
                        setpos(pos-2); first:=inchar;
                        if first='-' or first='+' then setpos(pos-1);
                     end else setpos(pos-1);
                     getNumber:=inint; ok:=true; end;
            end;
      end;

      text procedure getText(prompt); text prompt;
      inspect sysin do begin
            getImage(prompt); getText:-copy(sysin.image.strip);
      end;

      text edbuffer;

      text procedure edInt(i); integer i;
      begin text oldim;
            inspect sysout do begin
               oldim:-image; image:-edbuffer; image:=notext;
               setpos(1); outint(i,0); edInt:-copy(image.strip);
               image:-oldim;
            end;
      end;

      procedure outline(t); text t;
      begin outtext(t); outimage end;

      U1:=ClockTime;
      if rem(U1,2)=0 then begin if U1=0 then U1:=7913 else U1:=U1-1 end;
      U1:=abs(U1); U2:=U1+123; U3:=U1+1234567;
%+D   outint(U1,10); outint(U2,10); outint(U3,10); outimage; outimage;

      edbuffer:-copy(sysout.image); eject(1);
      outline("This is Anna Louise's arithmetic drill program");
      outline("==============================================");
      outimage; outimage;
      Anna:-getText("Who are you? "); outimage;
      if Anna.length > 5 and then lowcase(copy(Anna.sub(1,5)))="i am "
      then Anna:-copy(Anna.sub(6,Anna.length-5));
      upAnna:-upcase(copy(Anna));
      if Anna.length>1 then begin
         lowcase(Anna.sub(2,Anna.length-1)); upcase(Anna.sub(1,1)); end;

      okRes(0):-"You finally got it, baby!";
      okRes(1):-"BRAVO " & upAnna;
      okRes(2):-"That's RIGHT, " & Anna;
      okRes(3):-"You did it again, " & Anna;
      okRes(4):-Anna & ", your answer is CORRECT!!!";
      okRes(5):-"VERY GOOD, " & Anna;
      okRes(6):-"Yes, that's it!";
      okRes(7):-"RIGTIGT, " & Anna;
      okRes(8):-"Du er sandelig dygtig, " & Anna;
      okRes(9):-Anna & ", dit svar er KORREKT!!!";
      okRes(10):-"Meget meget fint, " & Anna;

      noRes(0):-"I'm sorry, " & Anna & ", wrong again";
      noRes(1):-upAnna & ", your answer is WRONG!!!";
      noRes(2):-"Oh boy - " & Anna & " - this was NOT a good guess";
      noRes(3):-"NO - NO - no .......";
      noRes(4):-"I don't think this is right, " & Anna & ", do you?";
      noRes(5):-"Mon ikke det var galt, " & Anna & ", om igen!";
      noRes(6):-"FORKERT, " & upAnna & " - paa den igen!";
      noRes(7):-"NEJ ... NEJ ... nej ...";
      noRes(8):-"Null putte, den gaar ikke!";

AGAIN: opertext:-notext;
      while opertext==notext do begin
            outline("Do you want to train ADDITION, SUBTRACTION,"
                    " or MULTIPLICATION?"); outimage;
           getImage("Type ................    +   or     -      "
                    "or         *    ....: ");
            operch:=inchar;
            if operch='-' then begin
               opertext:-" - "; helpoper:-"SUBTRACTIONS" end
       else if operch='+' then begin
               opertext:-" + "; helpoper:-"ADDITIONS" end
       else if operch='*' then begin
               opertext:-" * "; helpoper:-"MULTIPLICATIONS" end
            else opertext:- notext;
      end;

%           left:=randint(0,20,U1); right:=randint(1,17,U2);
%     outline("We are going to do some " & helpoper & "together.");
%     outline("We do it in this way. I will write a problem for ");
%     outline("You, and You write the answer (for instance, I write");
%     outline(edint(left) & opertext & edint(right) & " ?");
%     outline(" - and you type your answer, for instance 1");
%     outline("(I KNOW that you will not give this answer, because");
%     outline("it is wrong, but let's pretend ..."). 
%     outline(helpoper & ", we write it asnow that when we do ");
%     outline("We do it in this way. You know that when we do ");
%     outline("We do it in this way. You know that when we do ");
%              First YOU tell ME what are the");
%     outline("largest numbers you will try. If, for instance, we  ");
%     outline("are doing multiplications, then tell ME what are the");
%     outline("We do it in this way: First YOU tell ME what are the");
      
      outline("Which interval do you want, " & Anna & "?");
SAMEAGAIN:
      quest:=answer:=correct:=0; startR:=1; ! terminating:=false;
      lefthi:=getNumber(
      "Give me the highest number you want on the left... ");
      righthi:=getNumber(
      "Give me the highest number you want on the right... ");
      if righthi<0 or lefthi<0 then begin
         outline("- I cannot work with negative numbers");
         goto SAMEAGAIN;
      end;

      outline("OK, here we go, " & Anna); outimage;
      if operch='/' then rightlo:=1 else rightlo:=0;

      while true do begin
            left:=randint(0,lefthi,U1);
            right:=randint(rightlo,righthi,U2);
TRY:
            ires:=getNumber("What is " & edint(left) & opertext &
                            edint(right) & " ? ");
            quest:=quest+startR; answer:=answer+1;
            if okresult(left,right,ires) then begin
               outline(BRAVO & " - let's try another!"); end
            else begin outline(NONO); goto TRY end;
      end;

STOPIT:
      ! terminating:=true;
      outline("Your score was " & edint(correct) & 
              " out of " & edint(quest) & " possible");
      if quest=correct then begin eject(1); eject(10);
         sysout.setpos(10);
         outline("************************************");
         sysout.setpos(10);
         outline("***  YOU'VE GOT A PERFECT SCORE  ***");
         sysout.setpos(10);
         outline("************************************"); 
         eject(20); setpos(30); outline("kiss - kiss"); 
      end
      else if quest - correct >= correct then begin
         outline("  THIS IS TOO BAD, once again!");
         outimage; goto SAMEAGAIN end
      else if correct=0
      then outline("  You can do better than that!")
      else outline(" - that's not too bad!");
      outimage;
      while t=notext do begin
         t:-getText("Do you want to try something else?"
                    " (answer YES or NO) ");
         if t="YES" then goto AGAIN;
         if t<>"NO" then begin
            outline("""" & t & """" & " I cannot understand!");
            t:-notext end;
      end;
      outimage; outline("ok OK - then I will stop"); outimage;

end PROGRAM;
