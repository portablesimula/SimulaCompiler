begin short integer array q,u,d (-17:17);
      text OUTPOS, th; short integer i,j, nr; boolean nochk, all;
      character ch;

Procedure Setpos(Line,Pos); short integer Line,Pos;
begin if Line < 10 then
      begin OUTPOS.sub(4,1).putint(Line); OUTPOS.sub(3,1):="0";
      end else OUTPOS.sub(3,2).putint(Line);
      if Pos < 10 then
      begin OUTPOS.sub(7,1).putint(Pos); OUTPOS.sub(6,1):="0";
      end else OUTPOS.sub(6,2).putint(Pos);
      outtext(OUTPOS); breakoutimage;
end;

procedure drawboard;
begin outtext("!27![32m"); breakoutimage;
      Setpos(5,23);   outchar('!218!'); Setpos(5,55); outchar('!191!');
      Setpos(21,23);  outchar('!192!');
      Setpos(21,55); outchar('!217!'); breakoutimage;
      for i:= 1 step 1 until 9 do
      begin for j:= 1 step 1 until 8 do
            begin Setpos(3+2*i,20+4*j); outtext("!196!!196!!196!"); end;
            breakoutimage; end;
      for i:= 1 step 1 until 9 do
      begin for j:= 1 step 1 until 8 do
            begin Setpos(4+2*j,19+4*i); outchar('!179!'); end;
            breakoutimage; end;
      for i:= 1 step 1 until 7 do
      begin for j:= 1 step 1 until 7 do
            begin Setpos(5+2*j,23+4*i); outchar('!197!'); end;
            breakoutimage; end;
      for i:= 1 step 1 until 7 do
      begin Setpos(5,23+4*i); outchar('!194!'); end;  breakoutimage;
      for i:= 1 step 1 until 7 do
      begin Setpos(21,23+4*i); outchar('!193!'); end; breakoutimage;
      for i:= 1 step 1 until 7 do
      begin Setpos(5+2*i,23); outchar('!195!'); end;  breakoutimage;
      for i:= 1 step 1 until 7 do
      begin Setpos(5+2*i,55); outchar('!180!'); end; breakoutimage;
end Drawboard;

procedure ClearBoard;
begin for i:= 1 step 1 until 8 do
      begin for j:= 1 step 1 until 8 do
            begin Setpos(4+2*i,21+4*j); outchar(' '); end;
            Breakoutimage;
end;  end ClearBoard;

Integer procedure number(i,j,k,l); short integer i,j,k,l;
number:= ((i*8 + j)*8 + k)*8 + l;

short integer procedure qinv(i); short integer i;
begin short integer j;
      for j:= 1 step 1 until 8 do if q(j)=i then goto EX;
EX:   qinv:= j;
end qinv;

boolean procedure Exclude(q); value q; short integer array q;
begin integer thn; short integer i,j,k,l;
      i:= q(1); j:= q(2); k:= q(3); l:= q(4);
      thn:= number(i,j,k,l);
      if thn > number(9-i,9-j,9-k,9-l) then goto tr;
      i:= q(8); j:= q(7); k:= q(6); l:= q(5);
      if thn > number(i,j,k,l) then goto tr else
      if thn > number(9-i,9-j,9-k,9-l) then goto tr;
      i:= qinv(1); j:= qinv(2); k:= qinv(3); l:= qinv(4);
      if thn > number(i,j,k,l) then goto tr else
      if thn > number(9-i,9-j,9-k,9-l) then goto tr;
      i:= qinv(8); j:= qinv(7); k:= qinv(6); l:= qinv(5);
      if thn > number(i,j,k,l) then goto tr else
      if thn > number(9-i,9-j,9-k,9-l) then goto tr;
      Exclude:= false; goto EXIT;
TR:   Exclude:= true;
EXIT: end Exclude;

procedure p(r,c,q,u,d); value q,u,d;
short integer r,c; short integer array q,u,d;
if q(c)+u(r-c)+d(r+c) = 0 then
begin q(c):= u(r-c):= d(r+c):= r;
      for c:= 1 step 1 until 8 do if r<8 then p(r+1,c,q,u,d) else
      begin if c = 1 then ClearBoard;
            if nochk or else not Exclude(q) then else
            begin Setpos(4+2*c,21+4*q(c)); outchar('*'); Breakoutimage;
                  if c = 8 then 
                  begin nr:= nr + 1;
                        Setpos(24,23); outtext("Number: "); outint(nr,0);
                        Setpos(24,45); outtext("More? ");
                        Breakoutimage;
                        if not all then
                        begin inimage; ch:= inchar;
                              if ch = 'a' then all:= true else
                              if rank(ch)>32 then goto EXIT; end;
%                       ClearBoard;
end;  end;  end;  end;

OUTPOS:- copy("!27![01;01H");
outtext("!27![2J"); breakoutimage; Drawboard;
outtext("!27![33m"); breakoutimage;
Setpos(1,1); Outtext("      Take a Chessboard, place 8 queens on it");
outtext(" without any of them");
Setpos(2,1); Outtext("      able to strike another.");

% outtext("Only unique solutions? (Y)"); breakoutimage;
% inimage; ch:= inchar; if rank(ch)>32 then nochk:= true;
Drawboard;
outtext("!27![31m"); breakoutimage;
p(0,17,q,u,d);
EXIT:
Setpos(24,1); outtext("!27![0m"); breakoutimage;
end;
