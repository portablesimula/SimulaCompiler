begin 
%  Jensen's device exploits call by name and side-effects.
%  Call by name is an argument passing convention that delays the
%  evaluation of an argument until it is actually used in the
%  procedure (a consequence of the copy rule for procedures).
%  Algol introduced call by name and it was kept by Simula.
%  See: https://en.wikipedia.org/wiki/Jensen's_Device
 
 
long real procedure Sum(k, lower, upper, ak); 
      value lower, upper; name k, ak; 
      integer k, lower, upper; long real ak; 
      comment k and ak are passed by name; 
   begin 
      long real s; 
      s := 0.0; 
%     for k := lower step 1 until upper do ! Illegal in Simula;
      k := lower; 
      while k <= upper do 
      begin 
         s := s + ak; 
         k := k + 1; 
      end while;    
      Sum := s 
   end Sum; 
    
   integer i; 
   integer j,m,n; 
   long real array A(0:99); 
   long real result; 
   for j:=10 step 1 until 60 do A(j):=j; 
    
   outtext("-------------------------------------------------------------------"); outimage; 
   outtext("Sample Simula Program: Jensen's Device"); outimage; 
   outtext("First call on 'Sum' - Expected result: Sum=1785"); outimage; 
   result:=Sum(i,10,60,A(i)); 
   outtext("-------------------------------------------------------------------"); outimage; 
   outtext("End JensensDevice sample 1, Sum="); outint(result,6); outimage; 
    
   outtext("-------------------------------------------------------------------"); outimage; 
   outtext("Next line should be Runtime Error: Name Parameter is not a Variable"); outimage; 
   outtext("-------------------------------------------------------------------"); outimage; 
   result:=Sum(j+m*n,10,20,A(i)); 
    
end JensensDevice;

