
% =====================================   Main   program =============
%  PROGRAM NAME:   SIMULA A/S - SIMULA TEST NO 26
%
%  PURPOSE:     Test designational expressions, the goto statement
%               and switch declaration.
%
%  Flow:        T0 -> L1 -> T1 -> L2 -> T2 -> L3.
%
%  WRITTEN BY:  BEH   18/07-86
%  MODIFIED BY: MYH   24/07-2018
% ====================================================================
external class SimulaTest;

SimulaTest(26,"--- Test designational expressions, the goto statement and switch declaration.") begin

   integer i, j, t;
   boolean b,BBB;
   
   procedure trace(msg); text msg;
   begin
      if verbose then
      begin outtext(msg & " - t,i,j,b ="); 
         outint(t, 3); outint(i, 3); outint(j, 3);
         outtext(if b then " true" else " false");
         outimage;
      end;   
   end;

   i := 5;

   begin
      switch st := T1, T2;
%      switch s := L1, L2, q(i), if b imp j > i then q(2) else L1;
      switch s := L1, L2, q(i), if b imp j > i then q(2) else L2;
      switch q := s(1), q(4), q(2),
                  if if j=i then not b else b eqv b then L3 else s(2), q(1);
                  
                  
      text procedure Pst(ix); integer ix;
      begin  if ix=1 then Pst:-"T1"
        else if ix=2 then Pst:-"T2";
      end; 
       
      text procedure Ps(ix); integer ix;
      begin  if ix=1 then Ps:-"L1"
        else if ix=2 then Ps:-"L2"
        else if ix=3 then Ps:-Pq(i)
%        else if ix=4 then Ps:- if b imp j > i then Pq(2) else "L1"; 
        else if ix=4 then Ps:- if b imp j > i then Pq(2) else "L2"; 
      end;  
      
      text procedure Pq(ix); integer ix;
      begin  if ix=1 then Pq:-Ps(1)
        else if ix=2 then Pq:-Pq(4)
        else if ix=3 then Pq:-Pq(2)
        else if ix=4 then
        begin 
           outtext("IN TEXT PROCEDURE Pq: t,i,j,b=");
           outint(t, 3); outint(i, 3); outint(j, 3);
           outtext(if b then " true" else " false"); outimage;
           
           outtext("if j=i then not b else b eqv b EVALUTES "); 
           BBB:=if j=i then not b else b eqv b;
           outtext(if BBB then " true" else " false"); outimage;
           
           outtext("if j=i then not b else (b eqv b) EVALUTES "); 
           BBB:=if j=i then not b else (b eqv b);
           outtext(if BBB then " true" else " false"); outimage;
           
           outtext("(b eqv b) EVALUTES "); 
           BBB:=(b eqv b);
           outtext(if BBB then " true" else " false"); outimage;
           
           outtext("if if j=i then not b else b eqv b then L3 else Ps(2) EVALUTES "); 
           outtext(if if j=i then not b else b eqv b then "L3" else Ps(2)); outimage;

           Pq:-if if j=i then not b else b eqv b then "L3" else Ps(2)
        end
        else if i=5 then Pq:-Pq(1);
      end;

  T0: trace("AT T0 -- Should GOTO L1");
      if t <> 0 or i <> 5 or j <> 0 or b then
      begin
         err("Evaluation of designational expression,");
         outimage;
         outtext("           goto statement or program sequence ");
         outtext           ("control (1)."); 
         outimage;
         outtext("           Value of significant variables :");
         outimage;
         outtext("           ");
         outint(t, 3); outint(i, 3); outint(j, 3);
         outtext(if b then " true" else " false");
         outimage;
         
      end;

      t := 1;
      trace("Should GOTO " & Pq(i));
      go to q(i);


  T1: trace("AT T1 -- Should GOTO L2");
   if t <> 1 then
      begin
         err("Evaluation of designational expression,");
         outimage;
         outtext("           goto statement or program sequence ");
         outtext           ("control (2).");
         outimage;
         outtext("           Value of significant variables :");
         outimage;
         outtext("           ");
         outint(t, 3); outint(i, 3); outint(j, 3);
         outtext(if b then " true" else " false");
         outimage;
         
      end;

      t := 2;
      j := 5;
      b:=true;
      trace("Just before GOTO L2"); 
      trace("Should GOTO " & Ps(4));
      goto s(4);

  T2: trace("AT T2 -- Should GOTO L3");
   if t <> 2 or i <> 5 or j <> 5 or not b then
      begin
         err("Evaluation of designational expression,");
         outimage;
         outtext("           goto statement or program sequence ");
         outtext           ("control (3).");
         outimage;
         outtext("           Value of significant variables :");
         outimage;
         outtext("           ");
         outint(t, 3); outint(i, 3); outint(j, 3);
         outtext(if b then " true" else " false");
         outimage;
         
      end;

      t := j := i := 3; b:=false;
      trace("Just before GOTO L3"); 
      
      goto  s(3);


  L1: trace("AT L1 -- Should GOTO T1");
  
   if t <> 1 or i <> 5 or j <> 0 or b then
      begin
         err("Evaluation of designational expression,");
         outimage;
         outtext("           goto statement or program sequence ");
         outtext           ("control (4).");
         outimage;
         outtext("           Value of significant variables :");
         outimage;
         outtext("           ");
         outint(t, 3); outint(i, 3); outint(j, 3);
         outtext(if b then " true" else " false");
         outimage;
         
      end;

      begin switch r := s(1), q(4), s(3);
         begin character c;
            trace("Should GOTO " & Pst(t));
            goto st(t)
         end;
      end;

  L2: trace("AT L2 -- Should GOTO T2");
%   if t <> 2 or i <> 5 or j <> 5 or b then
   if t <> 2 or i <> 5 or j <> 5 or not b then
      begin
         err("Evaluation of designational expression,");
         outimage;
         outtext("           goto statement or program sequence ");
         outtext           ("control (5).");
         outimage;
         outtext("           Value of significant variables :");
         outimage;
         outtext("           ");
         outint(t, 3); outint(i, 3); outint(j, 3);
         outtext(if b then " true" else " false");
         outimage;
         
      end;

      b := true;
      goto st(t);


  L3: trace("AT L3 -- Should TERMINATE");
%   if t <> 3 or i <> 3 or j <> 3 or not b then
   if t <> 3 or i <> 3 or j <> 3 or b then
      begin
         err("Evaluation of designational expression,");
         outimage;
         outtext("           goto statement or program sequence ");
         outtext           ("control (6).");
         outimage;
         outtext("           Value of significant variables :");
         outimage;
         outtext("           ");
         outint(t, 3); outint(i, 3); outint(j, 3);
         outtext(if b then " true" else " false");
         outimage;
         
      end;
   end;

end;