
% ===============================================   Main   program =============
%  PROGRAM NAME:   Simula a.s. - SIMULA TEST NO 38
%
%  PURPOSE:     Test the value of type procedures.
%
%  NOTE:    1 - At line marked with NOTE 1, the acceptable relative deviation
%               should be filled in.
%  WRITTEN BY:  BEH   23/07-86
% ==============================================================================
external class SimulaTest;

SimulaTest(38,"--- Test the value of type procedures.") begin
   real eps;

   class A(t); value t; text t;;

   A class AB;;

   boolean       B;
   short integer si;
   integer       i;
   real          r;
   long real     lr;
   text          t;
   ref(A)       rA, rA2;
   ref(AB)      rB, rB2;

   boolean procedure PB(b); boolean b;
   if b then
   begin
      PB := true
   end;


   short integer procedure  Psi(b); boolean b;
   if b then
   begin
      Psi := 5
   end;

   integer procedure Pi(b); boolean b;
   if b then
   begin
      Pi := Psi(PB(true)) + 10
   end;


   real procedure Pr(b); boolean b;
   if b then
   begin
      Pr := Pi(true) * Psi(true)
   end;

   long real procedure Plr(b); boolean b;
   if b then
   begin
      Plr := Pr(true) * Psi(true) + Plr(false)
   end;

   text procedure Pt(b); boolean b;
   if b then
   begin
      text t;
      t :- blanks(5);
      t.putint(Pi(true));
      Pt :- t
   end;

   ref(A) procedure PrA(i); integer i;
   if i = 1 then
       PrA :- rA
   else
   if i = 2 then
      PrA :- new AB("ab");

   ref(AB) procedure PrAB(b); boolean b;
   if b then
   begin
      PrAB :- new AB("bb");
   end;


   eps := 0.001;                                         !***** NOTE 1 *****;

   B := PB(B);

   if B then begin
      err("Standard value of boolean procedure(1).");
      outtext("            Erroneus value : ");
      outtext(if B then "true" else "false");
      outimage;
   end;


   B := PB(B eqv B);

   if B then else begin
      err("Value of boolean procedure(2).");
      outtext("            Erroneus value : ");
      outtext(if B then "true" else "false");
      outimage;
   end;


   si := Psi(false);

   if si = 0 then else begin
      err("Standard value of short integer procedure(1).");
      outtext("            Erroneus value : ");
      outint(si, 5); outimage;
   end;

   si := Psi(true);

   if si = 5 then else begin
      err("value of short integer procedure(2).");
      outtext("            Erroneus value : ");
      outint(si, 5); outimage;
   end;


   i := Pi(B and not B);

   if i = 0 then else begin
      err("Standard value of integer procedure(3).");
      outtext("            Erroneus value : ");
      outint(i, 7); outimage;
   end;


   i := Pi(B or not B);

   if i = 15 then else begin
      err("value of integer procedure(4).");
      outtext("            Erroneus value : ");
      outint(i, 7); outimage;
   end;

   r :=  Pr(PB(PB(false)));

   if r = 0 then else begin
      err("Standard value of real procedure(5).");
      outtext("            Erroneus value : ");
      outreal(r, 4, 10); outimage;
   end;

   r := Pr(PB(B imp B));

   if r-r*eps > 75 or r+r*eps < 75 then begin
      err("value of real procedure(6).");
      outtext("            Erroneus value : ");
      outreal(r, 4, 10); outimage;
   end;


   lr := Plr(Pr(false) > 2);

   if lr = 0 then else begin
      err("Standard value of long real procedure(7).");
      outtext("            Erroneus value : ");
      outreal(lr, 4, 10); outimage;
   end;


   lr := Plr(true);

   if lr-lr*eps > 375 or lr+lr*eps < 375 then begin
      err("value of long real procedure(8).");
      outtext("            Erroneus value : ");
      outreal(lr, 4, 10); outimage;
   end;


   rA :- PrA(0);

   if rA == none then else begin
      err("Standard value of ref(A) procedure(9).");
      outtext("            Erroneus value : not none");
      outimage;
   end;


   rA :- new A("a");

   rA2 :- PrA(1);

   if rA2 == rA then else begin
      err("value of ref(A) procedure(10).");
      outtext("            Erroneus value : ");
      outtext(if rA2 == none then "Object none" else
              if rA2.t == notext then "Attribute = notext" else rA2.t );
   end;


   rA2 :- PrA(2);

   if rA2 =/= none and !then; rA2.t = "ab" then else begin
      err("Subordinate value of ref(A) procedure(11).");
      outtext("            Erroneus value : ");
      outtext(if rA2 == none then "object none" else
              if rA2.t == notext then "attribute = notext" else rA2.t );
   end;


   rB :- PrAB(false);

   if rB == none then else begin
      err("Standard value of ref(AB) procedure(12).");
      outtext("            Erroneus value : ");
      outtext(if rB == none then "Object none" else
              if rB.t == notext then "Attribute = notext" else rB.t );
   end;

   rB :- PrAB(rB2 == none);

   if rB =/= none and !then; rB.t = "bb" then else begin
      err("value of ref(AB) procedure(13).");
      outtext("            Erroneus value : ");
      outtext(if rB == none then "Object none" else
              if rB.t == notext then "Attribute = notext" else rB.t );
   end;

end;